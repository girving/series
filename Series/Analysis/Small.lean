import Mathlib.Analysis.Calculus.Taylor
import Series.Analysis.RCLike

/-!
# Low-order derivative zeros from smallness bounds
-/

open Set
open scoped ContDiff Topology

variable {ğ•œ : Type} [RCLike ğ•œ]
variable {E : Type} [NormedAddCommGroup E]
variable {n k : â„•}

/-- If a smooth function is small, its low-order derivatives are zero (`â„` version) -/
lemma Real.iteratedDeriv_eq_zero_of_small [NormedSpace â„ E] {f : â„ â†’ E} {c : â„}
    (df : ContDiffAt â„ n f c) (small : f =O[ğ“ c] fun x â†¦ â€–x - câ€– ^ n) (kn : k < n) :
    iteratedDeriv k f c = 0 := by
  induction' k using Nat.strong_induction_on with k low
  obtain âŸ¨u,un,df'âŸ© := df.contDiffOn (le_refl _) (by simp)
  obtain âŸ¨e,e0,subâŸ© := (nhds_basis_Icc_pos c).mem_iff.mp un
  have sub' : Icc c (c + e) âŠ† Icc (c - e) (c + e) := Icc_subset_Icc (by linarith) (by rfl)
  replace df' := df'.mono (sub'.trans sub)
  clear u un sub sub'
  obtain âŸ¨b',tayâŸ© := exists_taylor_mean_remainder_bound (f := f) (n := k) (by linarith)
    (df'.of_le (by norm_cast))
  generalize hb : max 0 b' = b
  have b0 : 0 â‰¤ b := by simp [â† hb]
  replace tay : âˆ€ x âˆˆ Icc c (c + e), â€–f x - taylorWithinEval f k (Icc c (c + e)) c xâ€– â‰¤
      b * (x - c) ^ (k + 1) := fun x m â†¦ le_trans (tay x m) (by simp at m; bound)
  clear hb b'
  have te : âˆ€ x, taylorWithinEval f k (Icc c (c + e)) c x =
      (x - c) ^ k â€¢ (k.factorial : â„)â»Â¹ â€¢ iteratedDeriv k f c := by
    intro x
    rw [taylor_within_apply, Finset.sum_eq_single (a := k)]
    Â· rw [smul_smul, mul_comm _ _â»Â¹, iteratedDerivWithin_eq_iteratedDeriv
        (uniqueDiffOn_Icc (by linarith)) (df.of_le (by norm_cast; omega)) (by simp [e0.le])]
    Â· intro j jk ne
      simp only [Finset.mem_range] at jk
      rw [iteratedDerivWithin_eq_iteratedDeriv (uniqueDiffOn_Icc (by linarith))
        (df.of_le (by norm_cast; omega)) (by simp [e0.le]), low _ (by omega) (by omega), smul_zero]
    Â· simp
  simp only [te] at tay
  suffices h : (k.factorial : â„)â»Â¹ â€¢ iteratedDeriv k f c = 0 by
    simpa [Nat.factorial_ne_zero] using h
  generalize (k.factorial : â„)â»Â¹ â€¢ iteratedDeriv k f c = a at tay
  clear te df' df low
  replace tay : âˆƒá¶  x in ğ“ c, x â‰  c âˆ§ â€–f x - (x - c) ^ k â€¢ aâ€– â‰¤ b * |x - c| ^ (k + 1) := by
    refine frequently_nhds_iff.mpr fun u cu uo â†¦ ?_
    obtain âŸ¨d,d0,subâŸ© := Metric.isOpen_iff.mp uo c cu
    refine âŸ¨c + min (e / 2) (d / 2), sub ?_, ?_, ?_âŸ©
    Â· simp only [Metric.mem_ball, dist_self_add_left, norm_eq_abs]
      rw [abs_of_nonneg (by bound), min_lt_iff]
      right
      bound
    Â· apply ne_of_gt (by simp; bound)
    Â· rw [abs_of_pos (by simp; bound)]
      apply tay
      simp [e0.le]
      bound
  obtain âŸ¨d,d0,small'âŸ© := Asymptotics.isBigO_iff'.mp small
  replace tay := tay.and_eventually small'
  clear small small' e0 e
  contrapose tay
  simp only [â† norm_pos_iff] at tay
  simp only [ne_eq, norm_eq_abs, norm_pow, abs_abs, Filter.not_frequently, not_and, not_le, and_imp]
  filter_upwards [eventually_norm_sub_lt c (Îµ := â€–aâ€– / (b + d)) (by bound),
    eventually_norm_sub_lt c (Îµ := 1) (by norm_num)] with x lt y1 xc le
  simp only [norm_eq_abs] at lt y1
  replace le : â€–aâ€– * |x - c| ^ k - b * |x - c| ^ (k + 1) â‰¤ â€–f xâ€– := by
    calc â€–f xâ€–
      _ = â€–(x - c) ^ k â€¢ a + (f x - (x - c) ^ k â€¢ a)â€– := by rw [add_sub_cancel]
      _ â‰¥ â€–(x - c) ^ k â€¢ aâ€– - â€–f x - (x - c) ^ k â€¢ aâ€– := by bound
      _ â‰¥ â€–(x - c) ^ k â€¢ aâ€– - b * |x - c| ^ (k + 1) := by bound
      _ = â€–aâ€– * |x - c| ^ k - b * |x - c| ^ (k + 1) := by simp [norm_smul, mul_comm]
  refine lt_of_lt_of_le ?_ le
  generalize hy : |x - c| = y at lt le y1
  have y0 : 0 < y := by simp only [â† hy, abs_pos, ne_eq]; simpa [sub_eq_zero]
  clear hy xc x le f
  rw [lt_sub_iff_add_lt]
  have ple : y ^ (n - k) â‰¤ y := pow_le_of_le_one y0.le y1.le (by omega)
  calc d * y ^ n + b * y ^ (k + 1)
    _ = (d * y ^ (n - k) + b * y) * y ^ k := by
        simp only [add_mul, mul_assoc, â† pow_succ', â† pow_add]
        rw [Nat.sub_add_cancel (by omega)]
    _ â‰¤ (d * y + b * y) * y ^ k := by bound
    _ = (b + d) * y * y ^ k := by ring
    _ < (b + d) * (â€–aâ€– / (b + d)) * y ^ k := by bound
    _ = â€–aâ€– * y ^ k := by rw [mul_div_cancelâ‚€]; positivity

/-- If a smooth function is small, its low-order derivatives are zero (`RCLike` version) -/
lemma iteratedDeriv_eq_zero_of_small [NormedSpace â„ E] [NormedSpace ğ•œ E] {f : ğ•œ â†’ E} {c : ğ•œ }
    (df : ContDiffAt ğ•œ n f c) (small : f =O[ğ“ c] fun x â†¦ â€–x - câ€– ^ n) (kn : k < n) :
    iteratedDeriv k f c = 0 := by
  rw [iteratedDeriv_eq_iteratedDeriv_real (df.of_le (by norm_cast; omega))]
  refine Real.iteratedDeriv_eq_zero_of_small ?_ ?_ kn
  Â· exact (df.restrict_scalars â„).comp_of_eq _ (by fun_prop) (by simp)
  Â· have cont : ContinuousAt (fun t : â„ â†¦ c + t) 0 := by fun_prop
    simp only [ContinuousAt, map_zero, add_zero] at cont
    simpa [Function.comp_def] using small.comp_tendsto cont
