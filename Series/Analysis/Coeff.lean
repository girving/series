import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.IteratedDeriv.Lemmas
import Series.Analysis.ContDiff

/-!
# Power series coefficients of functions
-/

open Finset (antidiagonal)
open Set
open scoped ContDiff Topology

variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]
variable {E : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {n : â„•}

/-- The `n`th coefficient of the power series of `f` -/
noncomputable def series_coeff (n : â„•) (f : ğ•œ â†’ E) (x : ğ•œ) : E :=
  (n.factorial : ğ•œ)â»Â¹ â€¢ iteratedDeriv n f x

@[simp] lemma series_coeff_zero {x : ğ•œ} : series_coeff n (0 : ğ•œ â†’ E) x = 0 := by
  simp only [series_coeff, iteratedDeriv_pi_zero, smul_zero]

@[simp] lemma series_coeff_zero' {f : ğ•œ â†’ E} {x : ğ•œ} : series_coeff 0 f x = f x := by
  simp only [series_coeff, Nat.factorial_zero, Nat.cast_one, inv_one, iteratedDeriv_zero, one_smul]

lemma series_coeff_succ {f : ğ•œ â†’ E} {x : ğ•œ} {n : â„•} :
    series_coeff (n + 1) f x = (n + 1 : ğ•œ)â»Â¹ â€¢ deriv (series_coeff n f) x:= by
  unfold series_coeff
  simp only [â† iteratedDeriv_succ, deriv_fun_const_smul', smul_smul]
  refine congr_argâ‚‚ _ ?_ rfl
  rw [â† mul_inv, inv_inj, â† Nat.cast_add_one, â† Nat.cast_mul, Nat.factorial_succ]

lemma ContDiffAt.series_coeff {f : ğ•œ â†’ E} {x : ğ•œ} {n m k : â„•} (df : ContDiffAt ğ•œ n f x)
    (le : m + k â‰¤ n) : ContDiffAt ğ•œ m (_root_.series_coeff k f) x :=
  (df.iteratedDeriv (by norm_cast)).const_smul _

/-- Prove a `series_coeff` is differentiable -/
macro "differentiable_series_coeff" : tactic =>
  `(tactic|exact (ContDiffAt.series_coeff (m := 1) (by assumption) (by omega)).differentiableAt
      (by simp only [Nat.cast_one, le_refl]))

lemma deriv_series_coeff [CharZero ğ•œ] {f : ğ•œ â†’ E} {x : ğ•œ} {k : â„•} :
    deriv (series_coeff k f) x = (k + 1 : ğ•œ) â€¢ series_coeff (k + 1) f x := by
  simp only [series_coeff_succ, smul_smul]
  rw [mul_inv_cancelâ‚€ (by norm_cast), one_smul]

/-- Power series multiply by convolution (i.e., as polynomials) -/
lemma series_coeff_mul [CharZero ğ•œ] {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} {n : â„•} (df : ContDiffAt ğ•œ n f x)
    (dg : ContDiffAt ğ•œ n g x) :
    series_coeff n (f * g) x =
      âˆ‘ p âˆˆ antidiagonal n, series_coeff p.1 f x * series_coeff p.2 g x := by
  induction' n with n h generalizing x
  Â· simp [series_coeff]
  Â· have e : series_coeff n (f * g) =á¶ [ğ“ x] fun y â†¦
        âˆ‘ p âˆˆ antidiagonal n, series_coeff p.1 f y * series_coeff p.2 g y := by
      filter_upwards [df.eventually (by simp), dg.eventually (by simp)] with y df dg
      specialize h (df.of_le (by simp)) (dg.of_le (by simp))
      rw [â† h, series_coeff]
    simp only [series_coeff_succ, e.deriv_eq]
    rw [deriv_fun_sum]
    Â· simp only [Finset.smul_sum]
      generalize ht : (fun (i j : â„•) â†¦ series_coeff i f x * series_coeff j g x) = t
      have ht' : âˆ€ i j, series_coeff i f x * series_coeff j g x = t i j := by simp [â† ht]
      simp only [smul_eq_mul, ht']
      trans âˆ‘ p âˆˆ antidiagonal n, (n + 1 : ğ•œ)â»Â¹ * (
        (p.1 + 1) * (t (p.1 + 1) p.2) + (p.2 + 1) * (t p.1 (p.2 + 1)))
      Â· refine Finset.sum_congr rfl fun p m â†¦ congr_argâ‚‚ _ rfl ?_
        simp only [Finset.mem_antidiagonal] at m
        rw [deriv_fun_mul (by differentiable_series_coeff) (by differentiable_series_coeff)]
        simp only [deriv_series_coeff, smul_eq_mul, â† ht]
        ring
      Â· trans âˆ‘ p âˆˆ antidiagonal (n + 1), (n + 1 : ğ•œ)â»Â¹ * (p.1 + p.2) * t p.1 p.2
        Â· simp only [â† Finset.mul_sum, mul_assoc]
          refine congr_argâ‚‚ _ rfl ?_
          simp only [add_mul ((_ : â„•) : ğ•œ) ((_ : â„•) : ğ•œ), Finset.sum_add_distrib]
          apply congr_argâ‚‚
          Â· simp [Finset.Nat.antidiagonal_succ]
          Â· simp [Finset.Nat.antidiagonal_succ']
        Â· refine Finset.sum_congr rfl fun p m â†¦ ?_
          simp only [Finset.mem_antidiagonal] at m
          norm_cast
          rw [m, inv_mul_cancelâ‚€, one_mul]
          norm_cast
    Â· intro i m
      simp only [Finset.mem_antidiagonal] at m
      apply DifferentiableAt.mul <;> differentiable_series_coeff

lemma series_coeff_const {x : ğ•œ} {s : E} {n : â„•} :
    series_coeff n (fun _ â†¦ s) x = if n = 0 then s else 0 := by
  induction' n with n h generalizing x
  Â· simp only [series_coeff, Nat.factorial_zero, Nat.cast_one, inv_one, iteratedDeriv_zero,
      one_smul, â†“reduceIte]
  Â· have e : series_coeff n (fun x â†¦ s) (ğ•œ := ğ•œ) = fun _ â†¦ if n = 0 then s else 0 := by
      ext x; simp only [h]
    simp only [series_coeff_succ, e, deriv_const', smul_zero, Nat.add_eq_zero, one_ne_zero,
      and_false, â†“reduceIte]

/-- `series_coeff` is local -/
lemma Filter.EventuallyEq.series_coeff_eq {f g : ğ•œ â†’ E} {x : ğ•œ} {n : â„•} (e : f =á¶ [ğ“ x] g) :
    series_coeff n f x = series_coeff n g x := by
  simp only [series_coeff, e.iteratedDeriv_eq]

/-!
### Machinery and notation for low-order series equality
-/

variable {f f' h g g' h' : ğ•œ â†’ E} {c : ğ•œ} {n m a b : â„•}

/-- Notation for two functions to match on low-order terms -/
def SeriesEq (f g : ğ•œ â†’ E) (n : â„•) (c : ğ•œ) : Prop :=
  âˆ€ i < n, ContDiffAt ğ•œ i f c âˆ§ ContDiffAt ğ•œ i g c âˆ§ series_coeff i f c = series_coeff i g c

notation:50 f " =Ë¢[" n "," c "] " g => SeriesEq f g n c
notation:50 f " =Ë¢[" n "] " g => SeriesEq f g n 0

-- Accessors
lemma SeriesEq.df (e : f =Ë¢[n,c] g) (i : â„•) (lt : i < n) : ContDiffAt ğ•œ i f c := (e i lt).1
lemma SeriesEq.dg (e : f =Ë¢[n,c] g) (i : â„•) (lt : i < n) : ContDiffAt ğ•œ i g c := (e i lt).2.1
lemma SeriesEq.eq (e : f =Ë¢[n,c] g) (i : â„•) (lt : i < n) : series_coeff i f c = series_coeff i g c :=
  (e i lt).2.2
lemma SeriesEq.iteratedDeriv_eq [CharZero ğ•œ] (e : f =Ë¢[n,c] g) (i : â„•) (lt : i < n) :
    iteratedDeriv i f c = iteratedDeriv i g c := by
  have h := e.eq i lt
  rwa [series_coeff, series_coeff, IsUnit.smul_left_cancel] at h
  simp [Nat.factorial_ne_zero]

/-- `SeriesEq` is reflexive for smooth functions -/
lemma SeriesEq.refl (df : âˆ€ i < n, ContDiffAt ğ•œ i f c) : f =Ë¢[n,c] f := by
  intro i lt
  exact âŸ¨df i lt, df i lt, rflâŸ©

/-- `n = 0` is trivial -/
@[simp] lemma SeriesEq.zero (f g : ğ•œ â†’ E) : f =Ë¢[0,c] g := by
  simp only [SeriesEq, not_lt_zero', IsEmpty.forall_iff, implies_true]

/-- `n = 0` is trivial -/
lemma SeriesEq.succ (f g : ğ•œ â†’ E) : (f =Ë¢[n + 1,c] g) â†” (f =Ë¢[n,c] g) âˆ§ ContDiffAt ğ•œ n f c âˆ§
    ContDiffAt ğ•œ n g c âˆ§ series_coeff n f c = series_coeff n g c := by
  simp only [SeriesEq, Nat.forall_lt_succ_right]

lemma SeriesEq.mono {f g : ğ•œ â†’ E} {a b : â„•} (lt : a â‰¤ b) (h : f =Ë¢[b,c] g) : f =Ë¢[a,c] g := by
  intro i lt
  exact h _ (by omega)

/-- `SeriesEq` is local on the left -/
lemma SeriesEq.congr_left (h : f =Ë¢[n,c] g) (e : f =á¶ [ğ“ c] f') : f' =Ë¢[n,c] g := by
  intro i lt
  obtain âŸ¨df,dg,ceâŸ© := h i lt
  exact âŸ¨df.congr_of_eventuallyEq e.symm, dg, e.series_coeff_eq â–¸ ceâŸ©

/-- `SeriesEq` is local on the left -/
lemma seriesEq_congr_left (e : f =á¶ [ğ“ c] f') : (f =Ë¢[n,c] g) â†” (f' =Ë¢[n,c] g) :=
  âŸ¨fun h â†¦ h.congr_left e, fun h â†¦ h.congr_left e.symmâŸ©

/-- Switching `SeriesEq` between `f = g` and `f - g = 0` -/
lemma seriesEq_sub_eq_zero_iff (dg : ContDiffAt ğ•œ m g c) (nm : n â‰¤ m + 1) :
    (f - g =Ë¢[n,c] 0) â†” (f =Ë¢[n,c] g) := by
  refine forallâ‚‚_congr fun i lt â†¦ ?_
  have dg : ContDiffAt ğ•œ i g c := dg.of_le (by simp; omega)
  simp only [series_coeff_zero, dg, true_and]
  constructor
  Â· intro âŸ¨ds,_,siâŸ©
    have e : f = f - g + g := by simp
    have df : ContDiffAt ğ•œ i f c := by
      exact e â–¸ ds.add dg
    simp only [df, true_and, series_coeff] at si âŠ¢
    simpa only [iteratedDeriv_sub df dg, smul_sub, sub_eq_zero] using si
  Â· intro âŸ¨df,fgiâŸ©
    refine âŸ¨df.sub dg, contDiffAt_const, ?_âŸ©
    simpa only [series_coeff, iteratedDeriv_sub df dg, smul_sub, sub_eq_zero]

/-- Zero orders add under multiplication -/
lemma SeriesEq.zero_mul_zero [CharZero ğ•œ] {f g : ğ•œ â†’ ğ•œ} (df : ContDiffAt ğ•œ m f c)
    (dg : ContDiffAt ğ•œ m g c) (f0 : f =Ë¢[a,c] 0) (g0 : g =Ë¢[b,c] 0) (nm : n â‰¤ m + 1)
    (ab : n â‰¤ a + b) :
    f * g =Ë¢[n,c] 0 := by
  intro i lt
  simp only [series_coeff_zero]
  refine âŸ¨(df.mul dg).of_le (by simp; omega), contDiffAt_const, ?_âŸ©
  --refine âŸ¨(df.mul dg).of_le (by norm_cast; simp; omega), contDiffAt_const, ?_âŸ©
  rw [series_coeff_mul]
  Â· apply Finset.sum_eq_zero fun p s â†¦ ?_
    simp only [Finset.mem_antidiagonal] at s
    have o : p.1 < a âˆ¨ p.2 < b := by omega
    rcases o with o | o
    Â· rw [f0.eq p.1 (by omega), series_coeff_zero, MulZeroClass.zero_mul]
    Â· rw [g0.eq p.2 (by omega), series_coeff_zero, MulZeroClass.mul_zero]
  Â· exact df.of_le (by simp; omega)
  Â· exact dg.of_le (by simp; omega)

/-- Zero orders multiply under powers -/
lemma SeriesEq.zero_pow [CharZero ğ•œ] {f : ğ•œ â†’ ğ•œ} (df : ContDiffAt ğ•œ m f c) (f0 : f =Ë¢[a,c] 0)
    (nm : n â‰¤ m + 1) (ab : n â‰¤ a * b) : f ^ b =Ë¢[n,c] 0 := by
  induction' b with b h generalizing n
  Â· simp only [mul_zero, nonpos_iff_eq_zero] at ab
    simp only [pow_zero, ab, zero]
  Â· simp only [pow_succ]
    refine zero_mul_zero (df.pow _) df (h (n := n - a) (by omega) ?_) f0 nm (by omega)
    simpa [â† mul_add_one]

lemma SeriesEq.zero_mul [CharZero ğ•œ] {f g : ğ•œ â†’ ğ•œ} (df : ContDiffAt ğ•œ m f c)
    (dg : ContDiffAt ğ•œ m g c) (h : f =Ë¢[n,c] 0) (nm : n â‰¤ m + 1) : f * g =Ë¢[n,c] 0 :=
  SeriesEq.zero_mul_zero df dg h (b := 0) (by simp) nm (by simp)

lemma SeriesEq.mul_zero [CharZero ğ•œ] {f g : ğ•œ â†’ ğ•œ} (df : ContDiffAt ğ•œ m f c)
    (dg : ContDiffAt ğ•œ m g c) (h : g =Ë¢[n,c] 0) (nm : n â‰¤ m + 1) : f * g =Ë¢[n,c] 0 := by
  rw [mul_comm]
  exact h.zero_mul dg df nm

lemma SeriesEq.trans (fg : f =Ë¢[n,c] g) (gh : g =Ë¢[n,c] h) : f =Ë¢[n,c] h := by
  intro i lt
  exact âŸ¨(fg i lt).1, (gh i lt).2.1, (fg i lt).2.2.trans (gh i lt).2.2âŸ©
