import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
# Power series coefficients of functions
-/

open Finset (antidiagonal)
open Set
open scoped Topology

variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]
variable {E : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- The `n`th coefficient of the power series of `f` -/
noncomputable def series_coeff (n : â„•) (f : ğ•œ â†’ E) (x : ğ•œ) : E :=
  (n.factorial : ğ•œ)â»Â¹ â€¢ iteratedDeriv n f x

lemma series_coeff_succ {f : ğ•œ â†’ E} {x : ğ•œ} {n : â„•} :
    series_coeff (n + 1) f x = (n + 1 : ğ•œ)â»Â¹ â€¢ deriv (series_coeff n f) x:= by
  unfold series_coeff
  simp only [â† iteratedDeriv_succ, deriv_fun_const_smul', smul_smul]
  refine congr_argâ‚‚ _ ?_ rfl
  rw [â† mul_inv, inv_inj, â† Nat.cast_add_one, â† Nat.cast_mul, Nat.factorial_succ]

lemma ContDiffAt.deriv {f : ğ•œ â†’ E} {x : ğ•œ} {n m : â„•} (df : ContDiffAt ğ•œ n f x)
    (le : m + 1 â‰¤ n) : ContDiffAt ğ•œ m (deriv f) x := by
  have e : _root_.deriv f = fun x â†¦ ContinuousLinearMap.apply ğ•œ E 1 (fderiv ğ•œ f x) := by
    ext x
    simp only [ContinuousLinearMap.apply_apply, fderiv_eq_smul_deriv, one_smul]
  rw [e]
  refine (df.fderiv_right ?_).continuousLinearMap_comp _
  norm_cast

lemma ContDiffAt.iteratedDeriv {f : ğ•œ â†’ E} {x : ğ•œ} {n m k : â„•} (df : ContDiffAt ğ•œ n f x)
    (le : m + k â‰¤ n) : ContDiffAt ğ•œ m (iteratedDeriv k f) x := by
  induction' k with k h generalizing m
  Â· simp only [add_zero] at le
    simp only [iteratedDeriv_zero]
    exact df.of_le (by simpa)
  Â· simp only [iteratedDeriv_succ]
    refine (h (m := m + 1) ?_).deriv ?_
    all_goals omega

lemma ContDiffAt.series_coeff {f : ğ•œ â†’ E} {x : ğ•œ} {n m k : â„•} (df : ContDiffAt ğ•œ n f x)
    (le : m + k â‰¤ n) : ContDiffAt ğ•œ m (_root_.series_coeff k f) x :=
  (df.iteratedDeriv le).const_smul _

/-- Prove a `series_coeff` is differentiable -/
macro "differentiable_series_coeff" : tactic =>
  `(tactic|exact (ContDiffAt.series_coeff (m := 1) (by assumption) (by omega)).differentiableAt
      (by simp only [Nat.cast_one, le_refl]))

lemma deriv_series_coeff [CharZero ğ•œ] {f : ğ•œ â†’ E} {x : ğ•œ} {k : â„•} :
    deriv (series_coeff k f) x = (k + 1 : ğ•œ) â€¢ series_coeff (k + 1) f x := by
  simp only [series_coeff_succ, smul_smul]
  rw [mul_inv_cancelâ‚€ (by norm_cast), one_smul]

/-- Power series multiply by convolution (i.e., as polynomials) -/
lemma series_coeff_mul [CharZero ğ•œ] {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} {n : â„•} (df : ContDiffAt ğ•œ n f x)
    (dg : ContDiffAt ğ•œ n g x) :
    series_coeff n (f * g) x =
      âˆ‘ p âˆˆ antidiagonal n, series_coeff p.1 f x * series_coeff p.2 g x := by
  induction' n with n h generalizing x
  Â· simp [series_coeff]
  Â· have e : series_coeff n (f * g) =á¶ [ğ“ x] fun y â†¦
        âˆ‘ p âˆˆ antidiagonal n, series_coeff p.1 f y * series_coeff p.2 g y := by
      filter_upwards [df.eventually (by simp), dg.eventually (by simp)] with y df dg
      specialize h (df.of_le (by simp)) (dg.of_le (by simp))
      rw [â† h, series_coeff]
    simp only [series_coeff_succ, e.deriv_eq]
    rw [deriv_fun_sum]
    Â· simp only [Finset.smul_sum]
      generalize ht : (fun (i j : â„•) â†¦ series_coeff i f x * series_coeff j g x) = t
      have ht' : âˆ€ i j, series_coeff i f x * series_coeff j g x = t i j := by simp [â† ht]
      simp only [smul_eq_mul, ht']
      trans âˆ‘ p âˆˆ antidiagonal n, (n + 1 : ğ•œ)â»Â¹ * (
        (p.1 + 1) * (t (p.1 + 1) p.2) + (p.2 + 1) * (t p.1 (p.2 + 1)))
      Â· refine Finset.sum_congr rfl fun p m â†¦ congr_argâ‚‚ _ rfl ?_
        simp only [Finset.mem_antidiagonal] at m
        rw [deriv_fun_mul (by differentiable_series_coeff) (by differentiable_series_coeff)]
        simp only [deriv_series_coeff, smul_eq_mul, â† ht]
        ring
      Â· trans âˆ‘ p âˆˆ antidiagonal (n + 1), (n + 1 : ğ•œ)â»Â¹ * (p.1 + p.2) * t p.1 p.2
        Â· simp only [â† Finset.mul_sum, mul_assoc]
          refine congr_argâ‚‚ _ rfl ?_
          simp only [add_mul ((_ : â„•) : ğ•œ) ((_ : â„•) : ğ•œ), Finset.sum_add_distrib]
          apply congr_argâ‚‚
          Â· simp [Finset.Nat.antidiagonal_succ]
          Â· simp [Finset.Nat.antidiagonal_succ']
        Â· refine Finset.sum_congr rfl fun p m â†¦ ?_
          simp only [Finset.mem_antidiagonal] at m
          norm_cast
          rw [m, inv_mul_cancelâ‚€, one_mul]
          norm_cast
    Â· intro i m
      simp only [Finset.mem_antidiagonal] at m
      apply DifferentiableAt.mul <;> differentiable_series_coeff

lemma series_coeff_const {x : ğ•œ} {s : E} {n : â„•} :
    series_coeff n (fun _ â†¦ s) x = if n = 0 then s else 0 := by
  induction' n with n h generalizing x
  Â· simp only [series_coeff, Nat.factorial_zero, Nat.cast_one, inv_one, iteratedDeriv_zero,
      one_smul, â†“reduceIte]
  Â· have e : series_coeff n (fun x â†¦ s) (ğ•œ := ğ•œ) = fun _ â†¦ if n = 0 then s else 0 := by
      ext x; simp only [h]
    simp only [series_coeff_succ, e, deriv_const', smul_zero, Nat.add_eq_zero, one_ne_zero,
      and_false, â†“reduceIte]
