import Interval.Unbundled
import Series.Pi
import Series.Series.Basic

/-!
# Multiplication of `Series`

We use https://en.wikipedia.org/wiki/Karatsuba_algorithm.
-/

open Finset (antidiagonal)
open Polynomial (C X)
open Set
open scoped Polynomial Topology

variable {Î± Î² : Type}

/-!
### Definitions
-/

section Defs

variable [Zero Î±] [Add Î±]

/-- Add two `Subarray`s of different size, forming an `Array`. The first must be the larger. -/
def Subarray.add_helper (f g : Subarray Î±) (_ : g.size â‰¤ f.size) : Array Î± :=
  .ofFn fun i : Fin f.size â†¦ f[i] + g.extend i

@[simp] lemma Subarray.size_add_helper (f g : Subarray Î±) (le : g.size â‰¤ f.size) :
    (f.add_helper g le).size = f.size := by simp [Subarray.add_helper]

variable [Sub Î±] [Mul Î±]

/-- Karatsuba multiplication of series, `Subarray` version -/
def Subarray.karatsuba (f g : Subarray Î±) (n : â„•) (fn : f.size = n) (gn : g.size â‰¤ n) : Array Î± :=
  -- Base cases
  if h : g.size = 0 then #[] else
  if h : g.size = 1 then .ofFn fun i : Fin f.size â†¦ f[i] * g[0] else
  if h : f.size = 2 then #[f[0] * g[0], f[0] * g[1] + f[1] * g[0], f[1] * g[1]] else
  let lo := (n + 1) / 2
  let hi := n - lo
  let f0 := f.take lo
  let f1 := f.drop lo
  let g0 := g.take lo
  let g1 := g.drop lo
  let p00 : Array Î± := f0.karatsuba g0 lo
    (by simp only [Subarray.size_take, f0]; omega)
    (by simp only [Subarray.size_take, g0]; omega)
  let p11 : Array Î± := f1.karatsuba g1 hi
    (by simp only [Subarray.size_drop, f1]; omega)
    (by simp only [Subarray.size_drop, g1]; omega)
  let fm := (f0.add_helper f1 (by simp [f0, f1]; omega)).toSubarray
  let gm := (g0.add_helper g1 (by simp [g0, g1]; omega)).toSubarray
  let pm := fm.karatsuba gm lo (by simp [fm, f0]; omega) (by simp [gm, g0])
  .ofFn fun i : Fin (n + (g.size - 1)) â†¦
    if i < lo then p00.extend i else
    if i < 2 * lo then (pm.extend (i - lo) + p00.extend i) -
                       (p00.extend (i - lo) + p11.extend (i - lo)) else
    if i < 3 * lo then (p11.extend (i - 2 * lo) + pm.extend (i - lo)) -
                       (p00.extend (i - lo) + p11.extend (i - lo)) else
    p11.extend (i - 2 * lo)

/-- Karatsuba multiplication of series, unordered `Subarray` version -/
@[irreducible] def Subarray.karatsuba' (f g : Subarray Î±) : Array Î± :=
  if h : g.size â‰¤ f.size then f.karatsuba g f.size rfl h
  else g.karatsuba f g.size rfl (by order)

/-- Karatsuba multiplication of `Series` -/
@[irreducible] def Series.mul (f g : Series Î±) : Series Î± :=
  let order := min f.order g.order
  let p := (f.c.takeLt order).karatsuba' (g.c.takeLt order)
  âŸ¨p.take (order.min_coe p.size), order, by simpâŸ©

/-- Karatsuba multiplication of `Series` -/
instance Series.instMul : Mul (Series Î±) where
  mul f g := f.mul g

lemma Series.mul_def (f g : Series Î±) : f * g = f.mul g := rfl

/-!
### Basic properties
-/

@[simp] lemma Subarray.size_karatsuba (f g : Subarray Î±) (n : â„•) (fn : f.size = n)
    (gn : g.size â‰¤ n) :
    (f.karatsuba g n fn gn).size = if g.size = 0 then 0 else f.size + g.size - 1 := by
  rw [karatsuba]
  split_ifs
  all_goals simp; try omega

@[simp] lemma Subarray.size_karatsuba' (f g : Subarray Î±) :
    (f.karatsuba' g).size = if f.size = 0 âˆ¨ g.size = 0 then 0 else f.size + g.size - 1 := by
  rw [karatsuba']
  simp only [apply_dite (f := fun f : Array Î± â†¦ f.size), size_karatsuba]
  grind

@[simp] lemma Series.order_mul {f g : Series Î±} : (f * g).order = min f.order g.order := by
  simp only [mul_def, mul]

lemma Series.size_mul {f g : Series Î±} :
    (f * g).c.size = if f.c.size = 0 âˆ¨ g.c.size = 0 then 0 else
      (min f.order g.order).min_coe (f.c.size + g.c.size - 1) := by
  simp only [mul_def, mul, Subarray.size_karatsuba', tsub_zero, ENat.min_coe_eq_zero_iff,
    Array.size_eq_zero_iff, Array.take_eq_extract, Array.size_extract, ENat.min_min_coe_right,
    Array.size_takeLt]
  generalize min f.order g.order = o
  split_ifs with h
  all_goals simp_all
  induction' o with o
  Â· simp only [ENat.top_min_coe]
  Â· simp only [Nat.cast_eq_zero, â† Array.size_eq_zero_iff] at h
    simp only [ENat.coe_min_coe]
    omega

@[simp] lemma Subarray.karatsuba'_empty {f g : Subarray Î±} (g0 : g.size = 0) :
    f.karatsuba' g = #[] := by
  rw [â† Array.size_eq_zero_iff]
  simp only [size_karatsuba', g0, or_true, â†“reduceIte]

@[simp] lemma Subarray.empty_karatsuba' {f g : Subarray Î±} (f0 : f.size = 0) :
    f.karatsuba' g = #[] := by
  rw [â† Array.size_eq_zero_iff]
  simp only [size_karatsuba', f0, true_or, â†“reduceIte]

end Defs

/-!
### Correctness, exact case

`Subarray.karatsuba` and `Series.mul` are polynomial multiplication.
-/

section Exact

variable {f g : Subarray Î±} {x : Î±} {n : â„•}

/-- `add_helper` is correct -/
@[simp] lemma Subarray.add_helper_extend [Zero Î±] [Add Î±] [AddZeroClass' Î±] (f g : Subarray Î±)
    (le : g.size â‰¤ f.size) (k : â„•) : (f.add_helper g le).extend k = f.extend k + g.extend k := by
  simp only [add_helper, Fin.getElem_fin, Array.extend_ofFn]
  split_ifs
  Â· simp only [Subarray.eq_extend]
  Â· rw [Subarray.extend_of_le (by omega), Subarray.extend_of_le (by omega), zero_add']

variable [CommRing Î±]

/-- Karatsuba multiplication of series, `Subarray` version -/
@[simp] lemma Subarray.poly_karatsuba (fn : f.size = n) (gn : g.size â‰¤ n) :
    (f.karatsuba g n fn gn).poly = f.poly * g.poly := by
  induction' n using Nat.strong_induction_on with n h generalizing f g
  rw [karatsuba]
  -- Induction base cases
  by_cases gs0 : g.size = 0
  Â· simp only [gs0, â†“reduceDIte, Array.poly_empty, poly_empty, mul_zero]
  simp only [gs0, dite_false]
  by_cases gs1 : g.size = 1
  Â· simp only [gs1, â†“reduceDIte, Fin.getElem_fin, poly_mul, poly_one, mul_comm f.poly, smul_one_mul]
  simp only [gs1, dite_false]
  by_cases fs2 : f.size = 2
  Â· have g2 : g.size = 2 := by omega
    simp [fs2, g2, poly_two, mul_add, add_mul, Array.poly, smul_mul_smul, â† pow_two, smul_smul,
      add_smul]
    ring_nf
  simp only [fs2, â†“reduceDIte, size_add_helper, size_take]
  -- Replace recursive Karatsuba calls
  have kt : âˆ€ g' n' fn' gn' i, ((f.take ((n + 1) / 2)).karatsuba g' n' fn' gn').extend i =
      ((f.take ((n + 1) / 2)).poly * g'.poly).coeff i := by
    intro g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have kd : âˆ€ g' n' fn' gn' i, ((f.drop ((n + 1) / 2)).karatsuba g' n' fn' gn').extend i =
      ((f.drop ((n + 1) / 2)).poly * g'.poly).coeff i := by
    intro g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have ks : âˆ€ a b ah g' n' fn' gn' i,
      ((((f.take ((n + 1) / 2)).add_helper (f.drop ((n + 1) / 2)) ah).toSubarray a b).karatsuba
        g' n' fn' gn').extend i =
      ((((f.take ((n + 1) / 2)).add_helper (f.drop ((n + 1) / 2)) ah).toSubarray a b).poly *
        g'.poly).coeff i := by
    intro a b ah g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have ap : âˆ€ (f' : Subarray Î±) ah, (((f'.take ((n + 1) / 2)).add_helper
      (f'.drop ((n + 1) / 2)) ah).toSubarray 0 (min f'.size ((n + 1) / 2))).poly =
      (f'.take ((n + 1) / 2)).poly + (f'.drop ((n + 1) / 2)).poly := by
    intro f' ah
    ext i
    rw [Polynomial.coeff_add, Array.poly_toSubarray, â† Array.extend_eq_coeff_poly]
    Â· simp only [add_helper_extend, extend_take, extend_drop, â† extend_eq_coeff_poly]
    Â· simp only [size_add_helper, size_take]
  simp only [kt, ks, kd, ap]
  clear kt ks kd ap h
  -- Replace with clean polynomials
  generalize hlo : (n + 1) / 2 = lo
  generalize hhi : n - lo = hi
  generalize hf0 : f.take lo = f0
  generalize hf1 : f.drop lo = f1
  generalize hg0 : g.take lo = g0
  generalize hg1 : g.drop lo = g1
  have fe : f.poly = f0.poly + f1.poly * X ^ lo := by simp only [â† hf0, â† hf1, split_poly]
  have ge : g.poly = g0.poly + g1.poly * X ^ lo := by simp only [â† hg0, â† hg1, split_poly]
  generalize hm : g.size = m
  have mn : m â‰¤ n := by omega
  have f0s : f0.size â‰¤ lo := by simp only [â† hf0, size_take]; omega
  have f1s : f1.size â‰¤ n - lo := by simp only [â† hf1, size_drop]; omega
  have g0s : g0.size â‰¤ lo := by simp only [â† hg0, size_take]; omega
  have g1s : g1.size â‰¤ m - lo := by simp only [â† hg1, size_drop]; omega
  have lo2 : 2 â‰¤ lo := by omega
  -- Descend into `Array.ofFn`
  ext i
  simp only [â† Array.extend_eq_coeff_poly, Array.extend_ofFn]
  clear gs0 gs1 fs2 hg0 hg1 hf0 hf1 hi hhi gn
  generalize hY : (X : Î±[X]) ^ lo = Y at fe ge
  have yd : Y.natDegree â‰¤ lo := by simp only [â† hY, Polynomial.natDegree_X_pow_le]
  have yd2 : (Y ^ 2).natDegree â‰¤ 2 * lo := by
    simp only [â† hY, â† pow_mul', Polynomial.natDegree_X_pow_le]
  -- Index lowering lemmas
  have low : âˆ€ (le : lo â‰¤ i := by omega) {f : Î±[X]}, f.coeff (i - lo) = (f * Y).coeff i :=
    fun le {f} â†¦ by simp only [â† hY, Polynomial.coeff_mul_X_pow', le, â†“reduceIte]
  have low2 : âˆ€ (le : 2 * lo â‰¤ i := by omega) {f : Î±[X]},
      f.coeff (i - 2 * lo) = (f * Y ^ 2).coeff i :=
    fun le {f} â†¦ by simp only [â† hY, â† pow_mul, mul_comm lo, Polynomial.coeff_mul_X_pow', le,
      â†“reduceIte]
  -- Zero lemmas
  have i_uy : âˆ€ (lt : i < lo := by omega) {u : Î±[X]}, (u * Y).coeff i = 0 := by
    intro lt u
    simp only [â† hY, Polynomial.coeff_mul_X_pow', not_le.mpr lt, â†“reduceIte]
  have i_uy2 : âˆ€ (lt : i < 2 * lo := by omega) {u : Î±[X]}, (u * Y ^ 2).coeff i = 0 := by
    intro lt u
    simp only [â† hY, Polynomial.coeff_mul_X_pow', not_le.mpr lt, â†“reduceIte, â† pow_mul']
  have uvz_i : âˆ€ (u v : Subarray Î±) {z : Î±[X]}
      (h : (u.size - 1) + (v.size - 1) + z.natDegree < i := by omega),
      (u.poly * v.poly * z).coeff i = 0 := by
    intro u v z h
    apply Polynomial.coeff_eq_zero_of_natDegree_lt
    calc (u.poly * v.poly * z).natDegree
      _ â‰¤ (u.poly * v.poly).natDegree + z.natDegree := by bound
      _ â‰¤ u.poly.natDegree + v.poly.natDegree + z.natDegree := by bound
      _ â‰¤ (u.size - 1) + (v.size - 1) + z.natDegree := by bound
      _ < i := h
  have uv_i : âˆ€ {u v : Subarray Î±} (h : u.size - 1 + (v.size - 1) < i := by omega),
      (u.poly * v.poly).coeff i = 0 := by
    intro u v h
    rw [â† mul_one (u.poly * v.poly)]
    exact uvz_i _ _ (by simp only [Polynomial.natDegree_one, add_zero, h])
  -- Case analysis
  split_ifs with h0 h1 h2 h3
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [i_uy, i_uy, i_uy2]
    ring
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [low, low, low, i_uy2]
    ring
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [low, low, low, low2, uv_i]
    ring
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [low2, uv_i, uvz_i f0 g1, uvz_i f1 g0]
    ring
  Â· rw [Polynomial.coeff_eq_zero_of_natDegree_lt]
    calc (f.poly * g.poly).natDegree
      _ â‰¤ f.poly.natDegree + g.poly.natDegree := by bound
      _ â‰¤ (f.size - 1) + (g.size - 1) := by bound
      _ < i := by omega

@[simp] lemma Subarray.poly_karatsuba' : (f.karatsuba' g).poly = f.poly * g.poly := by
  rw [karatsuba']
  split_ifs with h
  all_goals simp only [poly_karatsuba, mul_comm]

lemma Subarray.extend_karatsuba' : (f.karatsuba' g).extend = (f.poly * g.poly).coeff := by
  ext i
  simp only [Array.extend_eq_coeff_poly, poly_karatsuba']

/-- `Series.mul` is modular polynomial multiplication -/
@[simp] lemma Series.poly_mul [Nontrivial Î±] {f g : Series Î±} :
    (f * g).poly = (f.poly * g.poly).trunc (min f.order g.order) := by
  rw [mul_def, mul, Series.poly]
  simp only
  simp only [Subarray.size_karatsuba', Array.size_takeLt, ENat.min_coe_eq_zero_iff,
    Array.size_eq_zero_iff, Array.take_eq_extract, Array.poly_take, Subarray.poly_karatsuba',
    Array.poly_takeLt]
  repeat rw [Array.size_takeLt]
  generalize horder : min f.order g.order = order
  by_cases o0 : order = 0; Â· simp [o0]
  by_cases f0 : f.c.size = 0
  Â· simp only [Array.size_eq_zero_iff] at f0
    simp [f0, Series.poly]
  by_cases g0 : g.c.size = 0
  Â· simp only [Array.size_eq_zero_iff] at g0
    simp [g0, Series.poly]
  have e : order.min_coe (order.min_coe f.c.size + order.min_coe g.c.size - 1) =
      order.min_coe (f.c.size + g.c.size - 1) := by
    induction' order with o
    Â· simp only [ENat.top_min_coe]
    Â· simp only [ENat.coe_min_coe]; omega
  split_ifs with h
  Â· simp only [ENat.min_coe_eq_zero_iff, f0, or_false, g0, or_self] at h
    simp only [h, Polynomial.trunc_zero, mul_zero, ENat.min_coe_zero, pow_zero,
      Polynomial.modByMonic_one]
  simp only [e]
  ext i
  by_cases h : i < order
  Â· simp only [Polynomial.coeff_modByMnnic_X_pow, ENat.lt_min_coe_iff, h, true_and, if_true,
      Polynomial.coeff_mul, Polynomial.coeff_trunc, mul_ite, ite_mul, zero_mul, mul_zero]
    split_ifs with lt
    Â· refine Finset.sum_congr rfl fun âŸ¨a,bâŸ© m â†¦ ?_
      simp only [Finset.mem_antidiagonal] at m
      simp only [â† Array.extend_eq_coeff_poly, poly]
      have fle := f.le
      have gle := g.le
      split_ifs with h0 h1
      Â· rfl
      Â· simp only [not_lt] at h1
        have bad : (i : â„•âˆž) < a := by order
        rw [Nat.cast_lt] at bad
        omega
      Â· simp only [not_lt] at h0
        have bad : (i : â„•âˆž) < b := by order
        rw [Nat.cast_lt] at bad
        omega
    Â· symm
      refine Finset.sum_eq_zero fun âŸ¨a,bâŸ© m â†¦ ?_
      simp only [Finset.mem_antidiagonal] at m
      simp only [â† Array.extend_eq_coeff_poly, poly]
      have le : f.c.size â‰¤ a âˆ¨ g.c.size â‰¤ b := by omega
      rcases le with le | le
      all_goals simp only [Array.extend_of_le le, zero_mul, mul_zero]
  Â· simp only [Polynomial.coeff_modByMnnic_X_pow, ENat.lt_min_coe_iff, h, false_and, â†“reduceIte,
      Polynomial.coeff_trunc]

end Exact

/-!
### Karatsuba approximation

`Subarray.karatsuba` is a conservative approximation of polynomial multiplication.
-/

section KaratsubaApprox

variable [Zero Î±] [Add Î±] [AddZeroClass' Î±]
variable [Zero Î²] [Add Î²] [AddZeroClass' Î²]
variable [Approx Î± Î²] [ApproxAdd Î± Î²]
variable {f g : Subarray Î±} {f' g' : Subarray Î²} {n : â„•}

/-- `Subarray.add_helper` is conservative -/
@[approx] lemma Subarray.approx_add_helper_extend (fa : approx f.extend f'.extend)
    (ga : approx g.extend g'.extend) (le : g.size â‰¤ f.size) (le' : g'.size â‰¤ f'.size) :
    approx (f.add_helper g le).extend (f'.add_helper g' le').extend := by
  intro i
  simp only [add_helper_extend]
  exact approx_add (fa _) (ga _)

/-- `Subarray.add_helper` is conservative -/
@[approx] lemma Subarray.approx_add_helper [ApproxZero Î± Î²] (fa : approx f f') (ga : approx g g')
    (le : g.size â‰¤ f.size) (le' : g'.size â‰¤ f'.size) :
    approx (f.add_helper g le) (f'.add_helper g' le') := by
  refine âŸ¨?_, ?_âŸ©
  Â· simp only [size_add_helper, fa.1]
  Â· intro i lt
    simp only [Array.eq_extend, add_helper_extend]
    approx

variable [Sub Î±] [Mul Î±]
variable [Sub Î²] [Mul Î²]
variable [ApproxSub Î± Î²] [ApproxMul Î± Î²] [ApproxZero Î± Î²]

/-- Karasuba multiplication is conservative -/
@[approx] lemma Subarray.approx_karatsuba (fa : approx f f') (ga : approx g g') (fn : f.size = n)
    (gn : g.size â‰¤ n) (fn' : f'.size = n) (gn' : g'.size â‰¤ n) :
    approx (f.karatsuba g n fn gn) (f'.karatsuba g' n fn' gn') := by
  induction' n using Nat.strong_induction_on with n h generalizing f g f' g'
  have fs := fa.1
  have gs := ga.1
  refine âŸ¨by simp only [size_karatsuba, gs, fn, fn'], fun i lt â†¦ ?_âŸ©
  simp only [size_karatsuba, Array.eq_extend] at lt âŠ¢
  rw [karatsuba, karatsuba]
  by_cases g0 : g.size = 0
  Â· have g0' : g'.size = 0 := by simpa only [â† ga.1]
    simp only [g0, â†“reduceDIte, List.size_toArray, List.length_nil, zero_le, Array.extend_of_le,
      gs â–¸ g0, approx_zero]
  simp only [g0, â†“reduceDIte, Fin.getElem_fin, size_add_helper, size_take, gs â–¸ g0]
  by_cases g1 : g.size = 1
  Â· simp only [g1, â†“reduceDIte, Array.extend_ofFn, gs â–¸ g1]
    by_cases fi : i < f.size
    Â· simp only [fi, â†“reduceDIte, fs â–¸ fi]
      approx
    Â· simp only [fi, â†“reduceDIte, fs â–¸ fi, approx_zero]
  simp only [g1, â†“reduceDIte, gs â–¸ g1]
  by_cases f2 : f.size = 2
  Â· simp only [f2, â†“reduceDIte, fs â–¸ f2]
    approx
  simp only [f2, â†“reduceDIte, Array.extend_ofFn, dite_eq_ite, â† fs, â† gs]
  split_ifs with h0 h1
  all_goals try apply approx_sub
  all_goals try apply approx_add
  all_goals try apply approx_zero
  all_goals apply Array.approx_extend; apply h _ (by omega); all_goals approx

/-- Karasuba multiplication is conservative -/
@[approx] lemma Subarray.approx_karatsuba' (fa : approx f f') (ga : approx g g') :
    approx (f.karatsuba' g) (f'.karatsuba' g') := by
  rw [karatsuba', karatsuba']
  have fs := fa.1
  have gs := ga.1
  split_ifs with h h'
  Â· convert Subarray.approx_karatsuba fa ga rfl h (by omega) (by omega)
  Â· omega
  Â· omega
  Â· convert Subarray.approx_karatsuba ga fa rfl (by omega) (by omega) (by omega)

end KaratsubaApprox

/-!
### Series approximation

`Series` multiplication is a conservative approximation of function multiplication.
-/

section Approx

variable {ð•œ : Type} [NontriviallyNormedField ð•œ] [ApproxRing Î± ð•œ] [CharZero ð•œ]

lemma mul_order_rearrange (fo go : â„•âˆž) (fs gs : â„•) :
    ((min fo go).min_coe ((min fo go).min_coe fs + (min fo go).min_coe gs - 1)) =
      ((min fo go).min_coe ((min fo go).min_coe (fo.min_coe fs) +
                            (min fo go).min_coe (go.min_coe gs) - 1)) := by
  all_goals induction' fo with fo
  all_goals induction' go with go
  all_goals simp; try omega

/-- Exact series multiply as polynomials -/
lemma Series.exact_mul {f g : Series Î±} {f' g' : ð•œ â†’ ð•œ}
    (df : âˆ€ i : â„•, i < f.order â†’ ContDiffAt ð•œ i f' 0)
    (dg : âˆ€ i : â„•, i < g.order â†’ ContDiffAt ð•œ i g' 0)
    (f0 : âˆ€ i : â„•, f.c.size â‰¤ i â†’ i < f.order â†’ series_coeff i f' 0 = 0)
    (g0 : âˆ€ i : â„•, g.c.size â‰¤ i â†’ i < g.order â†’ series_coeff i g' 0 = 0) :
    exact (f' * g') (f * g).order (f * g).c.size =
      exact f' f.order f.c.size * exact g' g.order g.c.size := by
  simp only [order_mul]
  ext i lt
  Â· simp only [order_exact, order_mul]
  Â· simp only [size_mul, Array.size_eq_zero_iff, size_exact, ENat.min_coe_eq_zero_iff, order_exact]
    split_ifs with h0 h1 h2
    Â· aesop
    Â· aesop
    Â· aesop
    Â· have fle := f.le
      have gle := g.le
      generalize f.order = fo at fle
      generalize g.order = go at gle
      induction' fo with fo
      all_goals induction' go with go
      all_goals simp at fle gle âŠ¢
      all_goals omega
  Â· simp only [size_mul, size_exact, ENat.lt_min_coe_iff, lt_inf_iff,
      apply_ite (f := fun x â†¦ i < x), not_lt_zero', if_false_left, not_or] at lt
    obtain âŸ¨âŸ¨fi,giâŸ©,âŸ¨fs0,gs0âŸ©,_,ltâŸ© := lt
    simp only [exact, Array.extend_eq_coeff_poly, â† poly_def, poly_mul, Polynomial.coeff_trunc,
      lt_inf_iff, fi, gi, and_self, â†“reduceIte, Polynomial.coeff_mul]
    simp only [â† Array.extend_eq_coeff_poly, Array.size_ofFn, size_mul, fs0, gs0, or_self,
      â†“reduceIte, ENat.lt_min_coe_iff, lt_inf_iff, fi, gi, and_self, lt, Array.extend_of_lt,
      Array.getElem_ofFn, series_coeff_mul (df i fi) (dg i gi), poly_def, Array.extend_ofFn,
      dite_eq_ite, mul_ite, ite_mul, zero_mul, mul_zero]
    refine Finset.sum_congr rfl fun p m â†¦ ?_
    simp only [Finset.mem_antidiagonal] at m
    split_ifs with h0 h1
    Â· rfl
    Â· simp only [not_and, not_lt] at h1
      have lt : p.1 < f.order := lt_of_le_of_lt (by simp only [Nat.cast_le]; omega) fi
      rw [f0 _ (h1 lt) lt, zero_mul]
    Â· simp only [not_and, not_lt] at h0
      have lt : p.2 < g.order := lt_of_le_of_lt (by simp only [Nat.cast_le]; omega) gi
      rw [g0 _ (h0 lt) lt, mul_zero]

variable [AddZeroClass' Î±] [ApproxZeroIff Î± ð•œ]

/-- Series multiplication is conservative, function version -/
instance Series.instApproxMulFun : ApproxMul (Series Î±) (ð•œ â†’ ð•œ) where
  approx_mul {f g f' g'} fa ga := by
    apply approx_of_exact
    Â· intro i lt
      simp only [Series.order_mul, lt_inf_iff] at lt
      exact (fa i lt.1).1.mul (ga i lt.2).1
    Â· intro i le lt
      simp only [order_mul, lt_inf_iff] at lt
      rw [series_coeff_mul]
      Â· simp only [size_mul, apply_ite (f := fun x â†¦ x â‰¤ i), zero_le, if_true_left, not_or,
          and_imp, ENat.min_coe_le_iff, min_le_iff, not_le.mpr lt.1, not_le.mpr lt.2,
          false_or] at le
        refine Finset.sum_eq_zero fun âŸ¨a,bâŸ© m â†¦ ?_
        simp only [Finset.mem_antidiagonal, mul_eq_zero] at m âŠ¢
        have big : f.c.size â‰¤ a âˆ¨ g.c.size â‰¤ b := by omega
        rcases big with big | big
        Â· exact .inl (series_coeff_eq_zero fa a big (lt_of_le_of_lt (by simp; omega) lt.1))
        Â· exact .inr (series_coeff_eq_zero ga b big (lt_of_le_of_lt (by simp; omega) lt.2))
      Â· exact (fa i lt.1).1
      Â· exact (ga i lt.2).1
    Â· rw [exact_mul]
      Â· generalize ho : min f.order g.order = order
        simp only [mul_def, mul, ho, order_exact, Subarray.size_karatsuba', Series.approx_def,
          extend_def, Array.size_takeLt, ENat.min_coe_eq_zero_iff, size_exact, lt_min_iff]
        intro i âŸ¨io,_âŸ©
        have o0 : order â‰  0 := by contrapose io; simp_all
        have fo0 : f.order â‰  0 := by contrapose o0; simp_all
        have go0 : g.order â‰  0 := by contrapose o0; simp_all
        split_ifs with h0 h1 h2
        Â· simp only [o0, Array.size_eq_zero_iff, false_or] at h0
          rcases h0 with h0 | h0
          all_goals simp [h0]
        Â· simp only [o0, false_or, fo0, go0, not_or] at h1
          simp only [o0, h1, or_self] at h0
        Â· simp only [o0, false_or, not_or] at h0
          simp only [o0, fo0, h0, or_self, go0] at h2
        Â· apply Array.approx_extend
          have r := ho â–¸ mul_order_rearrange f.order g.order f.c.size g.c.size
          rw [â† r]
          apply Array.approx_take
          apply Subarray.approx_karatsuba'
          Â· refine âŸ¨by simp [â† ho], fun i lt â†¦ ?_âŸ©
            simp only [Array.size_takeLt, ENat.lt_min_coe_iff] at lt
            have flt : i < f.order := lt_of_lt_of_le lt.1 (by order)
            simp only [Subarray.eq_extend, lt.1, flt, lt.2, exact, Array.extend_takeLt,
              if_true, Array.extend_ofFn, ENat.lt_min_coe_iff, true_and, dite_true]
            exact (fa i flt).2
          Â· refine âŸ¨by simp [â† ho], fun i lt â†¦ ?_âŸ©
            simp only [Array.size_takeLt, ENat.lt_min_coe_iff] at lt
            have glt : i < g.order := lt_of_lt_of_le lt.1 (by order)
            simp only [Subarray.eq_extend, lt.1, glt, lt.2, exact, Array.extend_takeLt,
              if_true, Array.extend_ofFn, ENat.lt_min_coe_iff, true_and, dite_true]
            exact (ga i glt).2
      Â· intro i lt; exact (fa i lt).1
      Â· intro i lt; exact (ga i lt).1
      Â· intro i le lt; exact series_coeff_eq_zero fa _ le lt
      Â· intro i le lt; exact series_coeff_eq_zero ga _ le lt

end Approx
