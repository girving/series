import Series.Series.Basic

/-!
# Multiplication of `Series`

We use https://en.wikipedia.org/wiki/Karatsuba_algorithm.
-/

open Finset (antidiagonal)
open Polynomial (C X)
open Set
open scoped Polynomial Topology

variable {Î± Î² : Type}

/-!
### Definitions
-/

section Defs

variable [Zero Î±] [Add Î±]

/-- Add two `Subarray`s of different size, forming an `Array`. The first must be the larger. -/
def Subarray.add_helper (f g : Subarray Î±) (_ : g.size â‰¤ f.size) : Array Î± :=
  .ofFn fun i : Fin f.size â†¦ f[i] + g.extend i

@[simp] lemma Subarray.size_add_helper (f g : Subarray Î±) (le : g.size â‰¤ f.size) :
    (f.add_helper g le).size = f.size := by simp [Subarray.add_helper]

variable [Sub Î±] [Mul Î±]

/-- Karatsuba multiplication of series, `Subarray` version -/
def Subarray.karatsuba (f g : Subarray Î±) (n : â„•) (fn : f.size = n) (gn : g.size â‰¤ n) : Array Î± :=
  -- Base cases
  if h : g.size = 0 then #[] else
  if h : g.size = 1 then .ofFn fun i : Fin f.size â†¦ f[i] * g[0] else
  if h : f.size = 2 then #[f[0] * g[0], f[0] * g[1] + f[1] * g[0], f[1] * g[1]] else
  let lo := (n + 1) / 2
  let hi := n - lo
  let f0 := f.take lo
  let f1 := f.drop lo
  let g0 := g.take lo
  let g1 := g.drop lo
  let p00 : Array Î± := f0.karatsuba g0 lo
    (by simp only [Subarray.size_take, f0]; omega)
    (by simp only [Subarray.size_take, g0]; omega)
  let p11 : Array Î± := f1.karatsuba g1 hi
    (by simp only [Subarray.size_drop, f1]; omega)
    (by simp only [Subarray.size_drop, g1]; omega)
  let fm := (f0.add_helper f1 (by simp [f0, f1]; omega)).toSubarray
  let gm := (g0.add_helper g1 (by simp [g0, g1]; omega)).toSubarray
  let pm := fm.karatsuba gm lo (by simp [fm, f0]; omega) (by simp [gm, g0])
  .ofFn fun i : Fin (n + (g.size - 1)) â†¦
    if i < lo then p00.extend i else
    if i < 2 * lo then (pm.extend (i - lo) + p00.extend i) -
                       (p00.extend (i - lo) + p11.extend (i - lo)) else
    if i < 3 * lo then (p11.extend (i - 2 * lo) + pm.extend (i - lo)) -
                       (p00.extend (i - lo) + p11.extend (i - lo)) else
    p11.extend (i - 2 * lo)

/-- Karatsuba multiplication of `Series` -/
@[irreducible] def Series.mul (f g : Series Î±) : Series Î± :=
  let n := min f.n g.n
  let f' := f.c.toSubarray (stop := n)
  let g' := g.c.toSubarray (stop := n)
  -- TODO: Avoid unnecessarily computing coefficients beyond `n`
  let p := f'.karatsuba g' n (by simp [f', n, Series.n]) (by simp [g'])
  âŸ¨p.take nâŸ©

/-- Karatsuba multiplication of `Series` -/
instance Series.instMul : Mul (Series Î±) where
  mul f g := f.mul g

lemma Series.mul_def (f g : Series Î±) : f * g = f.mul g := rfl

/-!
### Basic properties
-/

@[simp] lemma Subarray.size_karatsuba (f g : Subarray Î±) (n : â„•) (fn : f.size = n)
    (gn : g.size â‰¤ n) :
    (f.karatsuba g n fn gn).size = if g.size = 0 then 0 else f.size + g.size - 1 := by
  rw [karatsuba]
  split_ifs
  all_goals simp; try omega

@[simp] lemma Series.n_mul {f g : Series Î±} : (f * g).n = min f.n g.n := by
  simp only [n, mul_def, mul, Array.take_eq_extract, Array.size_extract, Subarray.size_karatsuba,
    Array.size_toSubarray', inf_le_right, inf_of_le_right, tsub_zero, Nat.min_eq_zero_iff,
    Array.size_eq_zero_iff, inf_le_left, Nat.min_assoc]
  split_ifs with h
  all_goals simp only [Array.size_toSubarray'] at h; omega

end Defs

/-!
### Correctness, exact case

`Subarray.karatsuba` and `Series.mul` are polynomial multiplication.
-/

section Exact

variable [CommRing Î±] {f g : Subarray Î±} {x : Î±} {n : â„•}

/-- `add_helper` is correct -/
@[simp] lemma Subarray.add_helper_extend (f g : Subarray Î±) (le : g.size â‰¤ f.size)
    (k : â„•) : (f.add_helper g le).extend k = f.extend k + g.extend k := by
  simp only [add_helper, Fin.getElem_fin, Array.extend_ofFn]
  split_ifs
  Â· simp only [Subarray.eq_extend]
  Â· rw [Subarray.extend_of_le (by omega), Subarray.extend_of_le (by omega), zero_add]

/-- Karatsuba multiplication of series, `Subarray` version -/
@[simp] lemma Subarray.poly_karatsuba (fn : f.size = n) (gn : g.size â‰¤ n) :
    (f.karatsuba g n fn gn).poly = f.poly * g.poly := by
  induction' n using Nat.strong_induction_on with n h generalizing f g
  rw [karatsuba]
  -- Induction base cases
  by_cases gs0 : g.size = 0
  Â· simp only [gs0, â†“reduceDIte, Array.poly_empty, poly_empty, mul_zero]
  simp only [gs0, dite_false]
  by_cases gs1 : g.size = 1
  Â· simp only [gs1, â†“reduceDIte, Fin.getElem_fin, poly_mul, poly_one, mul_comm f.poly, smul_one_mul]
  simp only [gs1, dite_false]
  by_cases fs2 : f.size = 2
  Â· have g2 : g.size = 2 := by omega
    simp [fs2, g2, poly_two, mul_add, add_mul, Array.poly, smul_mul_smul, â† pow_two, smul_smul,
      add_smul]
    ring_nf
  simp only [fs2, â†“reduceDIte, size_add_helper, size_take]
  -- Replace recursive Karatsuba calls
  have kt : âˆ€ g' n' fn' gn' i, ((f.take ((n + 1) / 2)).karatsuba g' n' fn' gn').extend i =
      ((f.take ((n + 1) / 2)).poly * g'.poly).coeff i := by
    intro g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have kd : âˆ€ g' n' fn' gn' i, ((f.drop ((n + 1) / 2)).karatsuba g' n' fn' gn').extend i =
      ((f.drop ((n + 1) / 2)).poly * g'.poly).coeff i := by
    intro g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have ks : âˆ€ a b ah g' n' fn' gn' i,
      ((((f.take ((n + 1) / 2)).add_helper (f.drop ((n + 1) / 2)) ah).toSubarray a b).karatsuba
        g' n' fn' gn').extend i =
      ((((f.take ((n + 1) / 2)).add_helper (f.drop ((n + 1) / 2)) ah).toSubarray a b).poly *
        g'.poly).coeff i := by
    intro a b ah g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have ap : âˆ€ (f' : Subarray Î±) ah, (((f'.take ((n + 1) / 2)).add_helper
      (f'.drop ((n + 1) / 2)) ah).toSubarray 0 (min f'.size ((n + 1) / 2))).poly =
      (f'.take ((n + 1) / 2)).poly + (f'.drop ((n + 1) / 2)).poly := by
    intro f' ah
    ext i
    rw [Polynomial.coeff_add, Array.poly_toSubarray, â† Array.extend_eq_coeff_poly]
    Â· simp only [add_helper_extend, extend_take, extend_drop, â† extend_eq_coeff_poly]
    Â· simp only [size_add_helper, size_take]
  simp only [kt, ks, kd, ap]
  clear kt ks kd ap h
  -- Replace with clean polynomials
  generalize hlo : (n + 1) / 2 = lo
  generalize hhi : n - lo = hi
  generalize hf0 : f.take lo = f0
  generalize hf1 : f.drop lo = f1
  generalize hg0 : g.take lo = g0
  generalize hg1 : g.drop lo = g1
  have fe : f.poly = f0.poly + f1.poly * X ^ lo := by simp only [â† hf0, â† hf1, split_poly]
  have ge : g.poly = g0.poly + g1.poly * X ^ lo := by simp only [â† hg0, â† hg1, split_poly]
  generalize hm : g.size = m
  have mn : m â‰¤ n := by omega
  have f0s : f0.size â‰¤ lo := by simp only [â† hf0, size_take]; omega
  have f1s : f1.size â‰¤ n - lo := by simp only [â† hf1, size_drop]; omega
  have g0s : g0.size â‰¤ lo := by simp only [â† hg0, size_take]; omega
  have g1s : g1.size â‰¤ m - lo := by simp only [â† hg1, size_drop]; omega
  have lo2 : 2 â‰¤ lo := by omega
  -- Descend into `Array.ofFn`
  ext i
  simp only [â† Array.extend_eq_coeff_poly, Array.extend_ofFn]
  clear gs0 gs1 fs2 hg0 hg1 hf0 hf1 hi hhi gn
  generalize hY : (X : Î±[X]) ^ lo = Y at fe ge
  have yd : Y.natDegree â‰¤ lo := by simp only [â† hY, Polynomial.natDegree_X_pow_le]
  have yd2 : (Y ^ 2).natDegree â‰¤ 2 * lo := by
    simp only [â† hY, â† pow_mul', Polynomial.natDegree_X_pow_le]
  -- Index lowering lemmas
  have low : âˆ€ (le : lo â‰¤ i := by omega) {f : Î±[X]}, f.coeff (i - lo) = (f * Y).coeff i :=
    fun le {f} â†¦ by simp only [â† hY, Polynomial.coeff_mul_X_pow', le, â†“reduceIte]
  have low2 : âˆ€ (le : 2 * lo â‰¤ i := by omega) {f : Î±[X]},
      f.coeff (i - 2 * lo) = (f * Y ^ 2).coeff i :=
    fun le {f} â†¦ by simp only [â† hY, â† pow_mul, mul_comm lo, Polynomial.coeff_mul_X_pow', le,
      â†“reduceIte]
  -- Zero lemmas
  have i_uy : âˆ€ (lt : i < lo := by omega) {u : Î±[X]}, (u * Y).coeff i = 0 := by
    intro lt u
    simp only [â† hY, Polynomial.coeff_mul_X_pow', not_le.mpr lt, â†“reduceIte]
  have i_uy2 : âˆ€ (lt : i < 2 * lo := by omega) {u : Î±[X]}, (u * Y ^ 2).coeff i = 0 := by
    intro lt u
    simp only [â† hY, Polynomial.coeff_mul_X_pow', not_le.mpr lt, â†“reduceIte, â† pow_mul']
  have uvz_i : âˆ€ (u v : Subarray Î±) {z : Î±[X]}
      (h : (u.size - 1) + (v.size - 1) + z.natDegree < i := by omega),
      (u.poly * v.poly * z).coeff i = 0 := by
    intro u v z h
    apply Polynomial.coeff_eq_zero_of_natDegree_lt
    calc (u.poly * v.poly * z).natDegree
      _ â‰¤ (u.poly * v.poly).natDegree + z.natDegree := by bound
      _ â‰¤ u.poly.natDegree + v.poly.natDegree + z.natDegree := by bound
      _ â‰¤ (u.size - 1) + (v.size - 1) + z.natDegree := by bound
      _ < i := h
  have uv_i : âˆ€ {u v : Subarray Î±} (h : u.size - 1 + (v.size - 1) < i := by omega),
      (u.poly * v.poly).coeff i = 0 := by
    intro u v h
    rw [â† mul_one (u.poly * v.poly)]
    exact uvz_i _ _ (by simp only [Polynomial.natDegree_one, add_zero, h])
  -- Case analysis
  split_ifs with h0 h1 h2 h3
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [i_uy, i_uy, i_uy2]
    ring
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [low, low, low, i_uy2]
    ring
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [low, low, low, low2, uv_i]
    ring
  Â· simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, â† pow_two, Polynomial.coeff_add,
      â† mul_assoc _ _ Y]
    rw [low2, uv_i, uvz_i f0 g1, uvz_i f1 g0]
    ring
  Â· rw [Polynomial.coeff_eq_zero_of_natDegree_lt]
    calc (f.poly * g.poly).natDegree
      _ â‰¤ f.poly.natDegree + g.poly.natDegree := by bound
      _ â‰¤ (f.size - 1) + (g.size - 1) := by bound
      _ < i := by omega

/-- `Series.mul` is modular polynomial multiplication -/
@[simp] lemma Series.poly_mul [Nontrivial Î±] {f g : Series Î±} :
    (f * g).poly = f.poly * g.poly %â‚˜ X ^ min f.n g.n := by
  rw [mul_def, mul, Series.poly]
  simp only
  simp only [Array.take_eq_extract, Array.poly_take, Subarray.poly_karatsuba,
    Array.poly_toSubarray']
  ext i
  by_cases h : i < min f.n g.n
  Â· simp only [lt_inf_iff] at h
    simp only [Polynomial.coeff_modByMnnic_X_pow, lt_inf_iff, h, and_self, â†“reduceIte,
      Polynomial.coeff_mul, mul_ite, ite_mul, zero_mul, mul_zero]
    refine Finset.sum_congr rfl fun âŸ¨a,bâŸ© m â†¦ ?_
    simp only [Finset.mem_antidiagonal] at m
    split_ifs
    all_goals try simp; omega
    rfl
  Â· trans 0
    all_goals simp only [Polynomial.coeff_modByMnnic_X_pow, h, â†“reduceIte]

end Exact

/-!
### Karatsuba approximation

`Subarray.karatsuba` is a conservative approximation of polynomial multiplication.
-/

section KaratusbaApprox

variable [Zero Î±] [Add Î±]
variable [Zero Î²] [Add Î²]
variable [Approx Î± Î²] [ApproxZero Î± Î²] [ApproxAdd Î± Î²]
variable {f g : Subarray Î±} {f' g' : Subarray Î²} {n : â„•}

/-- `Subarray.add_helper` is conservative -/
@[approx] lemma Subarray.approx_add_helper (fa : approx f f') (ga : approx g g')
    (le : g.size â‰¤ f.size) (le' : g'.size â‰¤ f'.size) :
    approx (f.add_helper g le) (f'.add_helper g' le') := by
  refine âŸ¨?_, ?_âŸ©
  Â· simp only [size_add_helper, fa.1]
  Â· intro i lt
    simp only [add_helper, Fin.getElem_fin, Array.getElem_ofFn]
    approx

variable [Sub Î±] [Mul Î±] variable [Sub Î²] [Mul Î²]
variable [ApproxSub Î± Î²] [ApproxMul Î± Î²]

/-- Karasuba multiplication is conservative -/
@[approx] lemma Subarray.approx_karatsuba (fa : approx f f') (ga : approx g g') (fn : f.size = n)
    (gn : g.size â‰¤ n) (fn' : f'.size = n) (gn' : g'.size â‰¤ n) :
    approx (f.karatsuba g n fn gn) (f'.karatsuba g' n fn' gn') := by
  induction' n using Nat.strong_induction_on with n h generalizing f g f' g'
  have fs := fa.1
  have gs := ga.1
  refine âŸ¨by simp only [size_karatsuba, gs, fn, fn'], fun i lt â†¦ ?_âŸ©
  simp only [size_karatsuba, Array.eq_extend] at lt âŠ¢
  rw [karatsuba, karatsuba]
  by_cases g0 : g.size = 0
  Â· have g0' : g'.size = 0 := by simpa only [â† ga.1]
    simp only [g0, â†“reduceDIte, List.size_toArray, List.length_nil, zero_le, Array.extend_of_le,
      gs â–¸ g0, approx_zero]
  simp only [g0, â†“reduceDIte, Fin.getElem_fin, size_add_helper, size_take, gs â–¸ g0]
  by_cases g1 : g.size = 1
  Â· simp only [g1, â†“reduceDIte, Array.extend_ofFn, gs â–¸ g1]
    by_cases fi : i < f.size
    Â· simp only [fi, â†“reduceDIte, fs â–¸ fi]
      approx
    Â· simp only [fi, â†“reduceDIte, fs â–¸ fi, approx_zero]
  simp only [g1, â†“reduceDIte, gs â–¸ g1]
  by_cases f2 : f.size = 2
  Â· simp only [f2, â†“reduceDIte, fs â–¸ f2]
    approx
  simp only [f2, â†“reduceDIte, Array.extend_ofFn, dite_eq_ite, â† fs, â† gs]
  split_ifs with h0 h1
  all_goals try apply approx_sub
  all_goals try apply approx_add
  all_goals try apply approx_zero
  all_goals apply Array.approx_extend; apply h _ (by omega); all_goals approx

/-- Series multiplication is conservative, `Series` version -/
instance Series.instApproxMulSeries : ApproxMul (Series Î±) (Series Î²) where
  approx_mul {f g f' g'} fa ga := by
    simp only [mul_def, mul, approx_def, Series.n]
    simp only [â† fa.1, â† ga.1]
    approx

/-- `Series.mul` depends only on shared coefficients -/
lemma Series.mul_congr {f g f' g' : Series Î±} {n : â„•} (h : min f.n g.n = n) (h' : min f'.n g'.n = n)
    (fe : âˆ€ i < n, f.c.extend i = f'.c.extend i) (ge : âˆ€ i < n, g.c.extend i = g'.c.extend i) :
    f * g = f' * g' := by
  ext i lt
  Â· simp only [n_mul, h, h']
  Â· suffices a : approx (f * g) (f' * g') by simpa only [Array.eq_extend, approx] using a.2 _ lt
    simp only [mul_def, mul, approx_def, h, h']
    simp only [Series.n, min_eq_iff] at h h'
    have n0 : n â‰¤ f.c.size := by omega
    have n1 : n â‰¤ f'.c.size := by omega
    have n2 : n â‰¤ g.c.size := by omega
    have n3 : n â‰¤ g'.c.size := by omega
    apply Array.approx_take
    apply Subarray.approx_karatsuba
    all_goals simp [approx, n0, n1, n2, n3, Array.eq_extend]; assumption

end KaratusbaApprox

/-!
### Series approximation

`Series` multiplication is a conservative approximation of function multiplication.
-/

section Approx

variable {ð•œ : Type} [NontriviallyNormedField ð•œ] [CharZero ð•œ]
variable [ApproxRing Î± ð•œ]

/-- Exact series multiply as polynomials -/
lemma Series.exact_mul {f g : ð•œ â†’ ð•œ} {n : â„•} (df : âˆ€ i < n, ContDiffAt ð•œ i f 0)
    (dg : âˆ€ i < n, ContDiffAt ð•œ i g 0) : exact (f * g) n = exact f n * exact g n := by
  ext i lt
  Â· simp only [n_exact, n_mul, min_self]
  Â· simp only [n_exact] at lt
    simp only [exact, Array.size_ofFn, lt, Array.extend_of_lt, Array.getElem_ofFn,
      Array.extend_eq_coeff_poly, â† poly_def, poly_mul, n_mk, min_self,
      Polynomial.coeff_modByMnnic_X_pow, â†“reduceIte, Polynomial.coeff_mul]
    simp only [series_coeff_mul (df i lt) (dg i lt), poly_def, â† Array.extend_eq_coeff_poly,
      Array.extend_ofFn, dite_eq_ite, mul_ite, ite_mul, zero_mul, mul_zero]
    refine Finset.sum_congr rfl fun p m â†¦ ?_
    simp only [Finset.mem_antidiagonal] at m
    split_ifs
    Â· rfl
    Â· omega
    Â· omega

/-- Series multiplication is conservative, function version -/
instance Series.instApproxMulFun : ApproxMul (Series Î±) (ð•œ â†’ ð•œ) where
  approx_mul {f g f' g'} fa ga := by
    rw [Series.approx_iff_exact] at fa ga âŠ¢
    constructor
    Â· intro i lt
      simp only [Series.n_mul, lt_inf_iff] at lt
      exact (fa.1 i lt.1).mul (ga.1 i lt.2)
    Â· simp only [Series.n_mul]
      rw [Series.exact_mul]
      Â· rw [Series.mul_congr (f' := Series.exact f' f.n) (g' := Series.exact g' g.n)
          (n := min f.n g.n)]
        Â· approx
        Â· simp only [Series.n_exact, min_self]
        Â· simp only [Series.n_exact]
        Â· intro i lt; simp only [lt_inf_iff] at lt; simp [exact, lt]
        Â· intro i lt; simp only [lt_inf_iff] at lt; simp [exact, lt]
      Â· intro i lt; simp only [lt_inf_iff] at lt; exact fa.1 i lt.1
      Â· intro i lt; simp only [lt_inf_iff] at lt; exact ga.1 i lt.2

end Approx
