import Interval.Unbundled
import Series.Misc.Pi
import Series.Series.Basic

/-!
# Multiplication of `Series`

We use https://en.wikipedia.org/wiki/Karatsuba_algorithm.
-/

open Finset (antidiagonal)
open Polynomial (C X)
open Set
open scoped Polynomial Topology

variable {Œ± Œ≤ ùïú : Type}

/-!
### Definitions
-/

section Defs

variable [Zero Œ±] [Add Œ±]

/-- Add two `Subarray`s of different size, forming an `Array`. The first must be the larger. -/
def Subarray.add_helper (f g : Subarray Œ±) (_ : g.size ‚â§ f.size) : Array Œ± :=
  .ofFn fun i : Fin f.size ‚Ü¶ f[i] + g.extend i

@[simp] lemma Subarray.size_add_helper (f g : Subarray Œ±) (le : g.size ‚â§ f.size) :
    (f.add_helper g le).size = f.size := by simp [Subarray.add_helper]

variable [Sub Œ±] [Mul Œ±]

/-- Karatsuba multiplication of series, `Subarray` version -/
def Subarray.karatsuba (f g : Subarray Œ±) (n : ‚Ñï) (fn : f.size = n) (gn : g.size ‚â§ n) : Array Œ± :=
  -- Base cases
  if h : g.size = 0 then #[] else
  if h : g.size = 1 then .ofFn fun i : Fin f.size ‚Ü¶ f[i] * g[0] else
  if h : f.size = 2 then #[f[0] * g[0], f[0] * g[1] + f[1] * g[0], f[1] * g[1]] else
  let lo := (n + 1) / 2
  let hi := n - lo
  let f0 := f.take lo
  let f1 := f.drop lo
  let g0 := g.take lo
  let g1 := g.drop lo
  let p00 : Array Œ± := f0.karatsuba g0 lo
    (by simp only [Subarray.size_take, f0]; omega)
    (by simp only [Subarray.size_take, g0]; omega)
  let p11 : Array Œ± := f1.karatsuba g1 hi
    (by simp only [Subarray.size_drop, f1]; omega)
    (by simp only [Subarray.size_drop, g1]; omega)
  let fm := (f0.add_helper f1 (by simp [f0, f1]; omega)).toSubarray
  let gm := (g0.add_helper g1 (by simp [g0, g1]; omega)).toSubarray
  let pm := fm.karatsuba gm lo (by simp [fm, f0]; omega) (by simp [gm, g0])
  .ofFn fun i : Fin (n + (g.size - 1)) ‚Ü¶
    if i < lo then p00.extend i else
    if i < 2 * lo then (pm.extend (i - lo) + p00.extend i) -
                       (p00.extend (i - lo) + p11.extend (i - lo)) else
    if i < 3 * lo then (p11.extend (i - 2 * lo) + pm.extend (i - lo)) -
                       (p00.extend (i - lo) + p11.extend (i - lo)) else
    p11.extend (i - 2 * lo)

/-- Karatsuba multiplication of series, unordered `Subarray` version -/
@[irreducible] def Subarray.karatsuba' (f g : Subarray Œ±) : Array Œ± :=
  if h : g.size ‚â§ f.size then f.karatsuba g f.size rfl h
  else g.karatsuba f g.size rfl (by order)

/-- Karatsuba multiplication of `Series` -/
@[irreducible] def Series.mul (f g : Series Œ±) : Series Œ± :=
  let order := min f.order g.order
  let p := (f.c.takeLt order).karatsuba' (g.c.takeLt order)
  ‚ü®p.take (min order p.size), order, by simp‚ü©

/-- Karatsuba multiplication of `Series` -/
instance Series.instMul : Mul (Series Œ±) where
  mul f g := f.mul g

lemma Series.mul_def (f g : Series Œ±) : f * g = f.mul g := rfl

/-!
### Basic properties
-/

@[simp] lemma Subarray.size_karatsuba (f g : Subarray Œ±) (n : ‚Ñï) (fn : f.size = n)
    (gn : g.size ‚â§ n) :
    (f.karatsuba g n fn gn).size = if g.size = 0 then 0 else f.size + g.size - 1 := by
  rw [karatsuba]
  split_ifs
  all_goals simp; try omega

@[simp] lemma Subarray.size_karatsuba' (f g : Subarray Œ±) :
    (f.karatsuba' g).size = if f.size = 0 ‚à® g.size = 0 then 0 else f.size + g.size - 1 := by
  rw [karatsuba']
  simp only [apply_dite (f := fun f : Array Œ± ‚Ü¶ f.size), size_karatsuba]
  grind

@[simp] lemma Series.order_mul {f g : Series Œ±} : (f * g).order = min f.order g.order := by
  simp only [mul_def, mul]

lemma Series.size_mul {f g : Series Œ±} :
    (f * g).c.size = if f.c.size = 0 ‚à® g.c.size = 0 then 0 else
      min (min f.order g.order) (f.c.size + g.c.size - 1) := by
  simp only [mul_def, mul, Subarray.size_karatsuba', tsub_zero,
    Array.size_eq_zero_iff, Array.take_eq_extract, Array.size_extract,
    Array.size_takeLt]
  generalize min f.order g.order = o
  split_ifs with h
  all_goals simp_all
  simp only [‚Üê Array.size_eq_zero_iff] at h
  omega

@[simp] lemma Subarray.karatsuba'_empty {f g : Subarray Œ±} (g0 : g.size = 0) :
    f.karatsuba' g = #[] := by
  rw [‚Üê Array.size_eq_zero_iff]
  simp only [size_karatsuba', g0, or_true, ‚ÜìreduceIte]

@[simp] lemma Subarray.empty_karatsuba' {f g : Subarray Œ±} (f0 : f.size = 0) :
    f.karatsuba' g = #[] := by
  rw [‚Üê Array.size_eq_zero_iff]
  simp only [size_karatsuba', f0, true_or, ‚ÜìreduceIte]

end Defs

/-!
### Correctness, exact case

`Subarray.karatsuba` and `Series.mul` are polynomial multiplication.
-/

section Exact

variable {f g : Subarray Œ±} {x : Œ±} {n : ‚Ñï}

/-- `add_helper` is correct -/
@[simp] lemma Subarray.add_helper_extend [Zero Œ±] [Add Œ±] [AddZeroClass' Œ±] (f g : Subarray Œ±)
    (le : g.size ‚â§ f.size) (k : ‚Ñï) : (f.add_helper g le).extend k = f.extend k + g.extend k := by
  simp only [add_helper, Fin.getElem_fin, Array.extend_ofFn]
  split_ifs
  ¬∑ simp only [Subarray.eq_extend]
  ¬∑ rw [Subarray.extend_of_le (by omega), Subarray.extend_of_le (by omega), zero_add']

variable [CommRing Œ±]

/-- Karatsuba multiplication of series, `Subarray` version -/
@[simp] lemma Subarray.poly_karatsuba (fn : f.size = n) (gn : g.size ‚â§ n) :
    (f.karatsuba g n fn gn).poly = f.poly * g.poly := by
  induction' n using Nat.strong_induction_on with n h generalizing f g
  rw [karatsuba]
  -- Induction base cases
  by_cases gs0 : g.size = 0
  ¬∑ simp only [gs0, ‚ÜìreduceDIte, Array.poly_empty, poly_empty, mul_zero]
  simp only [gs0, dite_false]
  by_cases gs1 : g.size = 1
  ¬∑ simp only [gs1, ‚ÜìreduceDIte, Fin.getElem_fin, poly_mul, poly_one, mul_comm f.poly, smul_one_mul]
  simp only [gs1, dite_false]
  by_cases fs2 : f.size = 2
  ¬∑ have g2 : g.size = 2 := by omega
    simp [fs2, g2, poly_two, mul_add, add_mul, Array.poly, ‚Üê pow_two, smul_smul, add_smul]
    ring_nf
  simp only [fs2, ‚ÜìreduceDIte, size_add_helper, size_take]
  -- Replace recursive Karatsuba calls
  have kt : ‚àÄ g' n' fn' gn' i, ((f.take ((n + 1) / 2)).karatsuba g' n' fn' gn').extend i =
      ((f.take ((n + 1) / 2)).poly * g'.poly).coeff i := by
    intro g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have kd : ‚àÄ g' n' fn' gn' i, ((f.drop ((n + 1) / 2)).karatsuba g' n' fn' gn').extend i =
      ((f.drop ((n + 1) / 2)).poly * g'.poly).coeff i := by
    intro g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have ks : ‚àÄ a b ah g' n' fn' gn' i,
      ((((f.take ((n + 1) / 2)).add_helper (f.drop ((n + 1) / 2)) ah).toSubarray a b).karatsuba
        g' n' fn' gn').extend i =
      ((((f.take ((n + 1) / 2)).add_helper (f.drop ((n + 1) / 2)) ah).toSubarray a b).poly *
        g'.poly).coeff i := by
    intro a b ah g n fn gn i
    rw [Array.extend_eq_coeff_poly, h _ (by simp at fn; omega)]
  have ap : ‚àÄ (f' : Subarray Œ±) ah, (((f'.take ((n + 1) / 2)).add_helper
      (f'.drop ((n + 1) / 2)) ah).toSubarray 0 (min f'.size ((n + 1) / 2))).poly =
      (f'.take ((n + 1) / 2)).poly + (f'.drop ((n + 1) / 2)).poly := by
    intro f' ah
    ext i
    rw [Polynomial.coeff_add, Array.poly_toSubarray, ‚Üê Array.extend_eq_coeff_poly]
    ¬∑ simp only [add_helper_extend, extend_take, extend_drop, ‚Üê extend_eq_coeff_poly]
    ¬∑ simp only [size_add_helper, size_take]
  simp only [kt, ks, kd, ap]
  clear kt ks kd ap h
  -- Replace with clean polynomials
  generalize hlo : (n + 1) / 2 = lo
  generalize hhi : n - lo = hi
  generalize hf0 : f.take lo = f0
  generalize hf1 : f.drop lo = f1
  generalize hg0 : g.take lo = g0
  generalize hg1 : g.drop lo = g1
  have fe : f.poly = f0.poly + f1.poly * X ^ lo := by simp only [‚Üê hf0, ‚Üê hf1, split_poly]
  have ge : g.poly = g0.poly + g1.poly * X ^ lo := by simp only [‚Üê hg0, ‚Üê hg1, split_poly]
  generalize hm : g.size = m
  have mn : m ‚â§ n := by omega
  have f0s : f0.size ‚â§ lo := by simp only [‚Üê hf0, size_take]; omega
  have f1s : f1.size ‚â§ n - lo := by simp only [‚Üê hf1, size_drop]; omega
  have g0s : g0.size ‚â§ lo := by simp only [‚Üê hg0, size_take]; omega
  have g1s : g1.size ‚â§ m - lo := by simp only [‚Üê hg1, size_drop]; omega
  have lo2 : 2 ‚â§ lo := by omega
  -- Descend into `Array.ofFn`
  ext i
  simp only [‚Üê Array.extend_eq_coeff_poly, Array.extend_ofFn]
  clear gs0 gs1 fs2 hg0 hg1 hf0 hf1 hi hhi gn
  generalize hY : (X : Œ±[X]) ^ lo = Y at fe ge
  have yd : Y.natDegree ‚â§ lo := by simp only [‚Üê hY, Polynomial.natDegree_X_pow_le]
  have yd2 : (Y ^ 2).natDegree ‚â§ 2 * lo := by
    simp only [‚Üê hY, ‚Üê pow_mul', Polynomial.natDegree_X_pow_le]
  -- Index lowering lemmas
  have low : ‚àÄ (le : lo ‚â§ i := by omega) {f : Œ±[X]}, f.coeff (i - lo) = (f * Y).coeff i :=
    fun le {f} ‚Ü¶ by simp only [‚Üê hY, Polynomial.coeff_mul_X_pow', le, ‚ÜìreduceIte]
  have low2 : ‚àÄ (le : 2 * lo ‚â§ i := by omega) {f : Œ±[X]},
      f.coeff (i - 2 * lo) = (f * Y ^ 2).coeff i :=
    fun le {f} ‚Ü¶ by simp only [‚Üê hY, ‚Üê pow_mul, mul_comm lo, Polynomial.coeff_mul_X_pow', le,
      ‚ÜìreduceIte]
  -- Zero lemmas
  have i_uy : ‚àÄ (lt : i < lo := by omega) {u : Œ±[X]}, (u * Y).coeff i = 0 := by
    intro lt u
    simp only [‚Üê hY, Polynomial.coeff_mul_X_pow', not_le.mpr lt, ‚ÜìreduceIte]
  have i_uy2 : ‚àÄ (lt : i < 2 * lo := by omega) {u : Œ±[X]}, (u * Y ^ 2).coeff i = 0 := by
    intro lt u
    simp only [‚Üê hY, Polynomial.coeff_mul_X_pow', not_le.mpr lt, ‚ÜìreduceIte, ‚Üê pow_mul']
  have uvz_i : ‚àÄ (u v : Subarray Œ±) {z : Œ±[X]}
      (h : (u.size - 1) + (v.size - 1) + z.natDegree < i := by omega),
      (u.poly * v.poly * z).coeff i = 0 := by
    intro u v z h
    apply Polynomial.coeff_eq_zero_of_natDegree_lt
    calc (u.poly * v.poly * z).natDegree
      _ ‚â§ (u.poly * v.poly).natDegree + z.natDegree := by bound
      _ ‚â§ u.poly.natDegree + v.poly.natDegree + z.natDegree := by bound
      _ ‚â§ (u.size - 1) + (v.size - 1) + z.natDegree := by bound
      _ < i := h
  have uv_i : ‚àÄ {u v : Subarray Œ±} (h : u.size - 1 + (v.size - 1) < i := by omega),
      (u.poly * v.poly).coeff i = 0 := by
    intro u v h
    rw [‚Üê mul_one (u.poly * v.poly)]
    exact uvz_i _ _ (by simp only [Polynomial.natDegree_one, add_zero, h])
  -- Case analysis
  split_ifs with h0 h1 h2 h3
  ¬∑ simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, ‚Üê pow_two, Polynomial.coeff_add,
      ‚Üê mul_assoc _ _ Y]
    rw [i_uy, i_uy, i_uy2]
    ring
  ¬∑ simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, ‚Üê pow_two, Polynomial.coeff_add,
      ‚Üê mul_assoc _ _ Y]
    rw [low, low, low, i_uy2]
    ring
  ¬∑ simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, ‚Üê pow_two, Polynomial.coeff_add,
      ‚Üê mul_assoc _ _ Y]
    rw [low, low, low, low2, uv_i]
    ring
  ¬∑ simp only [fe, ge, mul_add, add_mul, mul_assoc _ Y, mul_comm Y, ‚Üê pow_two, Polynomial.coeff_add,
      ‚Üê mul_assoc _ _ Y]
    rw [low2, uv_i, uvz_i f0 g1, uvz_i f1 g0]
    ring
  ¬∑ rw [Polynomial.coeff_eq_zero_of_natDegree_lt]
    calc (f.poly * g.poly).natDegree
      _ ‚â§ f.poly.natDegree + g.poly.natDegree := by bound
      _ ‚â§ (f.size - 1) + (g.size - 1) := by bound
      _ < i := by omega

@[simp] lemma Subarray.poly_karatsuba' : (f.karatsuba' g).poly = f.poly * g.poly := by
  rw [karatsuba']
  split_ifs with h
  all_goals simp only [poly_karatsuba, mul_comm]

lemma Subarray.extend_karatsuba' : (f.karatsuba' g).extend = (f.poly * g.poly).coeff := by
  ext i
  simp only [Array.extend_eq_coeff_poly, poly_karatsuba']

/-- `Series.mul` is modular polynomial multiplication -/
@[simp] lemma Series.poly_mul [Nontrivial Œ±] {f g : Series Œ±} :
    (f * g).poly = (f.poly * g.poly).trunc (min f.order g.order) := by
  rw [mul_def, mul, Series.poly]
  simp only
  simp only [Subarray.size_karatsuba', Array.size_takeLt, ENat.coe_min_coe, Nat.min_assoc,
    Nat.min_eq_zero_iff, Array.size_eq_zero_iff, Array.take_eq_extract, Array.poly_take,
    Subarray.poly_karatsuba', Array.poly_takeLt, ENat.min_coe_coe]
  repeat rw [Array.size_takeLt]
  generalize horder : min f.order g.order = order
  by_cases o0 : order = 0; ¬∑ simp [o0]
  by_cases f0 : f.c.size = 0
  ¬∑ simp only [Array.size_eq_zero_iff] at f0
    simp [f0, Series.poly]
  by_cases g0 : g.c.size = 0
  ¬∑ simp only [Array.size_eq_zero_iff] at g0
    simp [g0, Series.poly]
  have e : min order (min order f.c.size + min order g.c.size - 1) =
      min order (f.c.size + g.c.size - 1) := by omega
  split_ifs with h
  ¬∑ simp only [ENat.coe_min_coe, Nat.min_eq_zero_iff, f0, or_false, g0, or_self] at h
    simp only [h, CharP.cast_eq_zero, Polynomial.trunc_zero, mul_zero, zero_le, inf_of_le_right,
      pow_zero, Polynomial.modByMonic_one]
  ext i
  by_cases h : i < order
  ¬∑ simp only [Polynomial.coeff_modByMnnic_X_pow, lt_min_iff, Polynomial.coeff_mul,
    Polynomial.coeff_trunc, mul_ite, ite_mul, zero_mul, mul_zero, Nat.cast_lt, h, ‚ÜìreduceIte]
    split_ifs with lt
    ¬∑ refine Finset.sum_congr rfl fun ‚ü®a,b‚ü© m ‚Ü¶ ?_
      have fle := f.le
      have gle := g.le
      aesop (add safe tactic (by omega))
    ¬∑ symm
      refine Finset.sum_eq_zero fun ‚ü®a,b‚ü© m ‚Ü¶ ?_
      simp only [Finset.mem_antidiagonal] at m
      simp only [not_and, not_lt, tsub_le_iff_right] at lt
      simp only [‚Üê Array.extend_eq_coeff_poly, Series.poly]
      have le : f.c.size ‚â§ a ‚à® g.c.size ‚â§ b := by omega
      rcases le with le | le
      all_goals simp only [Array.extend_of_le le, zero_mul, mul_zero]
  ¬∑ simp only [Polynomial.coeff_modByMnnic_X_pow, lt_inf_iff, Polynomial.coeff_trunc, Nat.cast_lt,
      h, ‚ÜìreduceIte, ite_eq_right_iff, and_imp]
    intro i0 i1
    simp only [‚Üê horder, lt_inf_iff, not_and, not_lt] at h
    omega

end Exact

/-!
### Karatsuba approximation

`Subarray.karatsuba` is a conservative approximation of polynomial multiplication.
-/

section KaratsubaApprox

variable [Zero Œ±] [Add Œ±] [AddZeroClass' Œ±]
variable [Zero Œ≤] [Add Œ≤] [AddZeroClass' Œ≤]
variable [Approx Œ± Œ≤] [ApproxAdd Œ± Œ≤]
variable {f g : Subarray Œ±} {f' g' : Subarray Œ≤} {n : ‚Ñï}

/-- `Subarray.add_helper` is conservative -/
@[approx] lemma Subarray.approx_add_helper_extend (fa : approx f.extend f'.extend)
    (ga : approx g.extend g'.extend) (le : g.size ‚â§ f.size) (le' : g'.size ‚â§ f'.size) :
    approx (f.add_helper g le).extend (f'.add_helper g' le').extend := by
  intro i
  simp only [add_helper_extend]
  exact approx_add (fa _) (ga _)

/-- `Subarray.add_helper` is conservative -/
@[approx] lemma Subarray.approx_add_helper [ApproxZero Œ± Œ≤] (fa : approx f f') (ga : approx g g')
    (le : g.size ‚â§ f.size) (le' : g'.size ‚â§ f'.size) :
    approx (f.add_helper g le) (f'.add_helper g' le') := by
  refine ‚ü®?_, ?_‚ü©
  ¬∑ simp only [size_add_helper, fa.1]
  ¬∑ intro i lt
    simp only [Array.eq_extend, add_helper_extend]
    approx

variable [Sub Œ±] [Mul Œ±]
variable [Sub Œ≤] [Mul Œ≤]
variable [ApproxSub Œ± Œ≤] [ApproxMul Œ± Œ≤] [ApproxZero Œ± Œ≤]

/-- Karasuba multiplication is conservative -/
@[approx] lemma Subarray.approx_karatsuba (fa : approx f f') (ga : approx g g') (fn : f.size = n)
    (gn : g.size ‚â§ n) (fn' : f'.size = n) (gn' : g'.size ‚â§ n) :
    approx (f.karatsuba g n fn gn) (f'.karatsuba g' n fn' gn') := by
  induction' n using Nat.strong_induction_on with n h generalizing f g f' g'
  have fs := fa.1
  have gs := ga.1
  refine ‚ü®by simp only [size_karatsuba, gs, fn, fn'], fun i lt ‚Ü¶ ?_‚ü©
  simp only [size_karatsuba, Array.eq_extend] at lt ‚ä¢
  rw [karatsuba, karatsuba]
  by_cases g0 : g.size = 0
  ¬∑ have g0' : g'.size = 0 := by simpa only [‚Üê ga.1]
    simp only [g0, ‚ÜìreduceDIte, List.size_toArray, List.length_nil, zero_le, Array.extend_of_le,
      gs ‚ñ∏ g0, approx_zero]
  simp only [g0, ‚ÜìreduceDIte, Fin.getElem_fin, size_add_helper, size_take, gs ‚ñ∏ g0]
  by_cases g1 : g.size = 1
  ¬∑ simp only [g1, ‚ÜìreduceDIte, Array.extend_ofFn, gs ‚ñ∏ g1]
    by_cases fi : i < f.size
    ¬∑ simp only [fi, ‚ÜìreduceDIte, fs ‚ñ∏ fi]
      approx
    ¬∑ simp only [fi, ‚ÜìreduceDIte, fs ‚ñ∏ fi, approx_zero]
  simp only [g1, ‚ÜìreduceDIte, gs ‚ñ∏ g1]
  by_cases f2 : f.size = 2
  ¬∑ simp only [f2, ‚ÜìreduceDIte, fs ‚ñ∏ f2]
    approx
  simp only [f2, ‚ÜìreduceDIte, Array.extend_ofFn, dite_eq_ite, ‚Üê fs, ‚Üê gs]
  split_ifs with h0 h1
  all_goals try apply approx_sub
  all_goals try apply approx_add
  all_goals try apply approx_zero
  all_goals apply Array.approx_extend; apply h _ (by omega); all_goals approx

/-- Karasuba multiplication is conservative -/
@[approx] lemma Subarray.approx_karatsuba' (fa : approx f f') (ga : approx g g') :
    approx (f.karatsuba' g) (f'.karatsuba' g') := by
  rw [karatsuba', karatsuba']
  have fs := fa.1
  have gs := ga.1
  split_ifs with h h'
  ¬∑ convert Subarray.approx_karatsuba fa ga rfl h (by omega) (by omega)
  ¬∑ omega
  ¬∑ omega
  ¬∑ convert Subarray.approx_karatsuba ga fa rfl (by omega) (by omega) (by omega)

end KaratsubaApprox

/-!
### Series approximation

`Series` multiplication is a conservative approximation of function multiplication.
-/

section Approx

variable [NontriviallyNormedField ùïú] [CharZero ùïú] [SeriesScalar Œ±]

lemma mul_order_rearrange (fo go : ‚Ñï) (fs gs : ‚Ñï) :
    (min (min fo go) (min (min fo go) fs + min (min fo go) gs - 1)) =
      (min (min fo go) (min (min fo go) (min fo fs) + min (min fo go) (min go gs) - 1)) := by
  omega

/-- Exact series multiply as polynomials -/
lemma Series.exact_mul {f g : Series Œ±} {f' g' : ùïú ‚Üí ùïú}
    (df : ‚àÄ i : ‚Ñï, i < f.order ‚Üí ContDiffAt ùïú i f' 0)
    (dg : ‚àÄ i : ‚Ñï, i < g.order ‚Üí ContDiffAt ùïú i g' 0)
    (f0 : ‚àÄ i : ‚Ñï, f.c.size ‚â§ i ‚Üí i < f.order ‚Üí series_coeff i f' 0 = 0)
    (g0 : ‚àÄ i : ‚Ñï, g.c.size ‚â§ i ‚Üí i < g.order ‚Üí series_coeff i g' 0 = 0) :
    exact (f' * g') (f * g).order (f * g).c.size =
      exact f' f.order f.c.size * exact g' g.order g.c.size := by
  simp only [order_mul]
  ext i lt
  ¬∑ simp only [order_exact, order_mul]
  ¬∑ simp only [size_mul, Array.size_eq_zero_iff, size_exact, Nat.min_eq_zero_iff, order_exact]
    split_ifs with h0 h1 h2
    ¬∑ aesop
    ¬∑ aesop
    ¬∑ aesop
    ¬∑ have fle := f.le
      have gle := g.le
      generalize f.order = fo at fle
      generalize g.order = go at gle
      induction' fo with fo
      all_goals induction' go with go
      all_goals simp at fle gle ‚ä¢
      all_goals omega
  ¬∑ simp only [size_mul, size_exact, lt_min_iff,
      apply_ite (f := fun x ‚Ü¶ i < x), not_lt_zero', if_false_left, not_or] at lt
    obtain ‚ü®‚ü®fi,gi‚ü©,‚ü®fs0,gs0‚ü©,_,lt‚ü© := lt
    simp only [exact, Array.extend_eq_coeff_poly, ‚Üê poly_def, poly_mul, Polynomial.coeff_trunc,
      lt_inf_iff, Polynomial.coeff_mul]
    simp only [‚Üê Array.extend_eq_coeff_poly, Array.size_ofFn, size_mul, fs0, gs0, or_self,
      ‚ÜìreduceIte, lt_min_iff, fi, gi, and_self, lt, Array.extend_of_lt,
      Array.getElem_ofFn, series_coeff_mul (df i fi) (dg i gi), poly_def, Array.extend_ofFn,
      dite_eq_ite, mul_ite, ite_mul, zero_mul, mul_zero, Nat.cast_lt]
    refine Finset.sum_congr rfl fun p m ‚Ü¶ ?_
    simp only [Finset.mem_antidiagonal] at m
    split_ifs with h0 h1
    ¬∑ rfl
    ¬∑ simp only [not_and, not_lt] at h1
      have lt : p.1 < f.order := lt_of_le_of_lt (by omega) fi
      rw [f0 _ (h1 lt) lt, zero_mul]
    ¬∑ simp only [not_and, not_lt] at h0
      have lt : p.2 < g.order := lt_of_le_of_lt (by omega) gi
      rw [g0 _ (h0 lt) lt, mul_zero]

/-- Series multiplication is conservative, function version -/
instance Series.instApproxMulFun [ApproxSeries Œ± ùïú] : ApproxMul (Series Œ±) (ùïú ‚Üí ùïú) where
  approx_mul {f g f' g'} fa ga := by
    apply approx_of_exact
    ¬∑ intro i lt
      simp only [Series.order_mul, lt_inf_iff] at lt
      exact (fa i lt.1).1.mul (ga i lt.2).1
    ¬∑ intro i le lt
      simp only [order_mul, lt_inf_iff] at lt
      rw [series_coeff_mul]
      ¬∑ simp only [size_mul, apply_ite (f := fun x ‚Ü¶ x ‚â§ i), zero_le, if_true_left, not_or,
          and_imp, min_le_iff, min_le_iff, not_le.mpr lt.1, not_le.mpr lt.2,
          false_or] at le
        refine Finset.sum_eq_zero fun ‚ü®a,b‚ü© m ‚Ü¶ ?_
        simp only [Finset.mem_antidiagonal, mul_eq_zero] at m ‚ä¢
        have big : f.c.size ‚â§ a ‚à® g.c.size ‚â§ b := by omega
        rcases big with big | big
        ¬∑ exact .inl (series_coeff_eq_zero fa a big (lt_of_le_of_lt (by omega) lt.1))
        ¬∑ exact .inr (series_coeff_eq_zero ga b big (lt_of_le_of_lt (by omega) lt.2))
      ¬∑ exact (fa i lt.1).1
      ¬∑ exact (ga i lt.2).1
    ¬∑ rw [exact_mul]
      ¬∑ generalize ho : min f.order g.order = order
        simp only [mul_def, mul, ho, order_exact, Subarray.size_karatsuba', Series.approx_def,
          extend_def, Array.size_takeLt, size_exact, lt_min_iff, Nat.min_eq_zero_iff,
          ENat.coe_min_coe]
        intro i ‚ü®io,_‚ü©
        have o0 : order ‚â† 0 := by contrapose io; simp_all
        have fo0 : f.order ‚â† 0 := by contrapose o0; simp_all
        have go0 : g.order ‚â† 0 := by contrapose o0; simp_all
        split_ifs with h0 h1 h2
        ¬∑ simp only [o0, Array.size_eq_zero_iff, false_or] at h0
          rcases h0 with h0 | h0
          all_goals simp [h0]
        ¬∑ simp only [o0, false_or, fo0, go0, not_or] at h1
          simp only [o0, h1, or_self] at h0
        ¬∑ simp only [o0, false_or, not_or] at h0
          simp only [o0, fo0, h0, or_self, go0] at h2
        ¬∑ apply Array.approx_extend
          have r := ho ‚ñ∏ mul_order_rearrange f.order g.order f.c.size g.c.size
          rw [‚Üê r]
          apply Array.approx_take
          apply Subarray.approx_karatsuba'
          ¬∑ refine ‚ü®by simp [‚Üê ho]; omega, fun i lt ‚Ü¶ ?_‚ü©
            simp only [Array.size_takeLt, ENat.lt_min_coe_iff] at lt
            have flt : i < f.order := lt_of_lt_of_le lt.1 (by order)
            simp only [Subarray.eq_extend, lt.1, flt, lt.2, exact, Array.extend_takeLt,
              if_true, Array.extend_ofFn, lt_min_iff, true_and, dite_true]
            exact (fa i flt).2
          ¬∑ refine ‚ü®by simp [‚Üê ho], fun i lt ‚Ü¶ ?_‚ü©
            simp only [Array.size_takeLt, ENat.lt_min_coe_iff] at lt
            have glt : i < g.order := lt_of_lt_of_le lt.1 (by order)
            simp only [Subarray.eq_extend, lt.1, glt, lt.2, exact, Array.extend_takeLt,
              if_true, Array.extend_ofFn, lt_min_iff, true_and, dite_true]
            exact (ga i glt).2
      ¬∑ intro i lt; exact (fa i lt).1
      ¬∑ intro i lt; exact (ga i lt).1
      ¬∑ intro i le lt; exact series_coeff_eq_zero fa _ le lt
      ¬∑ intro i le lt; exact series_coeff_eq_zero ga _ le lt

end Approx
