import Interval.Unbundled
import Mathlib.Analysis.Calculus.IteratedDeriv.Lemmas
import Series.Series.Basic
import Series.Series.Const

/-!
# Module structure on `Series`
-/

open Polynomial (X)
open Set
open scoped Polynomial Topology ENat
namespace Series

variable {Î± Î² : Type}
variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]

/-!
### Definitions
-/

@[irreducible] def neg [Neg Î±] [Zero Î±] (f : Series Î±) : Series Î± :=
  f.map (-Â·)

@[irreducible] def add [Add Î±] [Zero Î±] (f g : Series Î±) : Series Î± :=
  let order := min f.order g.order
  let n := order.min_coe (max f.c.size g.c.size)
  âŸ¨.ofFn fun i : Fin n â†¦ f.extend i + g.extend i, order, by simp [n]âŸ©

@[irreducible] def sub [Sub Î±] [Zero Î±] (f g : Series Î±) : Series Î± :=
  let order := min f.order g.order
  let n := order.min_coe (max f.c.size g.c.size)
  âŸ¨.ofFn fun i : Fin n â†¦ f.extend i - g.extend i, order, by simp [n]âŸ©

@[irreducible] def smul [SMul Î± Î²] [Zero Î²] (s : Î±) (f : Series Î²) : Series Î² :=
  f.map (s â€¢ Â·)

instance [Neg Î±] [Zero Î±] : Neg (Series Î±) := âŸ¨negâŸ©
instance [Add Î±] [Zero Î±] : Add (Series Î±) := âŸ¨addâŸ©
instance [Sub Î±] [Zero Î±] : Sub (Series Î±) := âŸ¨subâŸ©
instance [SMul Î± Î²] [Zero Î²] : SMul Î± (Series Î²) := âŸ¨smulâŸ©

/-!
### Basic properties
-/

lemma neg_def [Neg Î±] [Zero Î±] (f : Series Î±) : -f = f.neg := rfl
lemma add_def [Add Î±] [Zero Î±] (f g : Series Î±) : f + g = f.add g := rfl
lemma sub_def [Sub Î±] [Zero Î±] (f g : Series Î±) : f - g = f.sub g := rfl
lemma smul_def [SMul Î± Î²] [Zero Î²] (s : Î±) (f : Series Î²) : s â€¢ f = f.smul s := rfl

@[simp] lemma order_neg [Neg Î±] [Zero Î±] (f : Series Î±) : (-f).order = f.order := by
  rw [neg_def, neg, order_map]
@[simp] lemma order_smul [SMul Î± Î²] [Zero Î²] (s : Î±) (f : Series Î²) : (s â€¢ f).order = f.order := by
  rw [smul_def, smul, order_map]
@[simp] lemma order_add [Add Î±] [Zero Î±] (f g : Series Î±) :
    (f + g).order = min f.order g.order := by rw [add_def, add]
@[simp] lemma order_sub [Sub Î±] [Zero Î±] (f g : Series Î±) :
    (f - g).order = min f.order g.order := by rw [sub_def, sub]

/-!
### Approx instances
-/

instance instApproxNeg [Zero Î±] [Neg Î±] [NegZeroClass' Î±] [Approx Î± ğ•œ] [ApproxNeg Î± ğ•œ] :
    ApproxNeg (Series Î±) (ğ•œ â†’ ğ•œ) where
  approx_neg {f f'} m k lt := by
    simp only [neg_def, neg, order_map, series_coeff, iteratedDeriv_neg, smul_eq_mul,
      mul_neg] at k lt âŠ¢
    obtain âŸ¨c, aâŸ© := m k lt
    refine âŸ¨c.neg, ?_âŸ©
    rw [getElem_map neg_zero']
    approx

instance instApproxAdd [Zero Î±] [Add Î±] [AddZeroClass' Î±] [Approx Î± ğ•œ] [ApproxAdd Î± ğ•œ] :
    ApproxAdd (Series Î±) (ğ•œ â†’ ğ•œ) where
  approx_add {f g f' g'} fa ga k lt := by
    simp only [add_def, add, extend_def, lt_min_iff, Array.extend_ofFn, ENat.lt_min_coe_iff,
      lt_sup_iff, dite_eq_ite] at k lt âŠ¢
    simp only [lt, and_self, true_and]
    obtain âŸ¨fc, faâŸ© := fa k lt.1
    obtain âŸ¨gc, gaâŸ© := ga k lt.2
    have e : (if k < f.c.size âˆ¨ k < g.c.size then f.c.extend k + g.c.extend k else 0) =
        f.c.extend k + g.c.extend k := by
      split_ifs with h
      Â· rfl
      Â· simp only [not_or, not_lt] at h
        simp only [h, Array.extend_of_le, add_zero']
    simp only [iteratedDeriv_add fc gc, smul_add, series_coeff, e]
    exact âŸ¨fc.add gc, by approxâŸ©

instance instApproxSub [Zero Î±] [Neg Î±] [Sub Î±] [SubZeroClass Î±] [Approx Î± ğ•œ] [ApproxSub Î± ğ•œ] :
    ApproxSub (Series Î±) (ğ•œ â†’ ğ•œ) where
  approx_sub {f g f' g'} fa ga k lt := by
    simp only [sub_def, sub, extend_def, lt_min_iff, Array.extend_ofFn, ENat.lt_min_coe_iff,
      lt_sup_iff, dite_eq_ite] at k lt âŠ¢
    simp only [lt, and_self, true_and]
    obtain âŸ¨fc, faâŸ© := fa k lt.1
    obtain âŸ¨gc, gaâŸ© := ga k lt.2
    have e : (if k < f.c.size âˆ¨ k < g.c.size then f.c.extend k - g.c.extend k else 0) =
        f.c.extend k - g.c.extend k := by
      split_ifs with h
      Â· rfl
      Â· simp only [not_or, not_lt] at h
        simp only [h, Array.extend_of_le, sub_zero']
    simp only [iteratedDeriv_sub fc gc, smul_sub, series_coeff, e]
    exact âŸ¨fc.sub gc, by approxâŸ©

instance instApproxSMul [Approx Î² ğ•œ] [Zero Î²] [SMulZeroClass Î± Î²] [Approx Î± ğ•œ]
    [ApproxSMul Î± Î² ğ•œ ğ•œ] : ApproxSMul Î± (Series Î²) ğ•œ (ğ•œ â†’ ğ•œ) where
  approx_smul {s f s' f'} sa fa k lt := by
    simp only [smul_def, smul, order_map] at k lt âŠ¢
    obtain âŸ¨fc, faâŸ© := fa k lt
    simp only [iteratedDeriv_const_smul fc, smul_comm _ s, series_coeff]
    refine âŸ¨fc.const_smul s, ?_âŸ©
    rw [getElem_map (smul_zero _)]
    approx
